const std = @import("std");
const runCommand = @import("run.zig").runCommand;
const Target = @import("backend").Target;

pub fn run(
    compiler_path: []const u8,
    file_name: []const u8,
    snapshot_name: []const u8,
    update: bool,
    target: Target,
    alloc: std.mem.Allocator,
    io: std.Io,
) !void {
    std.debug.print("running {s}", .{file_name});
    const host_arg = try std.fmt.allocPrint(
        alloc,
        "--host={s}",
        .{try target.host.toString()},
    );
    defer alloc.free(host_arg);
    const device_arg = try std.fmt.allocPrint(
        alloc,
        "--device={s}",
        .{try target.device.toString()},
    );
    defer alloc.free(device_arg);

    const result = try runCommand(alloc, io, &.{
        compiler_path,
        file_name,
        "/tmp/out.s",
        "--run",
        "--dump-user-stats",
        "--omit-escape-codes",
        "--optim",
        host_arg,
        device_arg,
    });
    defer alloc.free(result.stdout);
    defer alloc.free(result.stderr);

    // snapshot dir
    const snapshot_dir_path = try std.fmt.allocPrint(
        alloc,
        "tst/snapshot/{s}",
        .{try target.host.toString()},
    );
    defer alloc.free(snapshot_dir_path);

    const dir = try std.Io.Dir.cwd().createDirPathOpen(
        io,
        snapshot_dir_path,
        .{ .open_options = .{ .iterate = true } },
    );
    defer dir.close(io);
    const snapshot_file_name = try std.fmt.allocPrint(alloc, "{s}.snapshot", .{snapshot_name});
    defer alloc.free(snapshot_file_name);

    // verify
    const temp_file_path_with_dir = try std.fs.path.join(alloc, &.{ "/tmp", snapshot_file_name });
    defer alloc.free(temp_file_path_with_dir);
    const temp_file = try std.Io.Dir.createFileAbsolute(io, temp_file_path_with_dir, .{});
    defer temp_file.close(io);
    try temp_file.writeStreamingAll(io, result.stderr);

    const snapshot_file_name_with_dir = try std.fs.path.join(alloc, &.{ snapshot_dir_path, snapshot_file_name });
    defer alloc.free(snapshot_file_name_with_dir);
    const diff = try std.process.run(alloc, io, .{ .argv = &.{
        "git",
        "diff",
        "--no-index",
        "--color=always",
        "--",
        snapshot_file_name_with_dir,
        temp_file_path_with_dir,
    } });
    defer alloc.free(diff.stdout);
    defer alloc.free(diff.stderr);

    const code = diff.term.exited;

    // perform snapshotting
    if (update) {
        if (code == 0) {
            std.debug.print(" [[EQUAL]]\n", .{});
            return;
        }
        const file = try dir.createFile(io, snapshot_file_name, .{});
        defer file.close(io);

        var file_buf: [1028]u8 = undefined;
        var file_writer: std.Io.File.Writer = .init(file, io, &file_buf);
        try file_writer.interface.writeAll(result.stderr);
        try file_writer.interface.flush();
        std.debug.print(" [[REGENERATED]]\n", .{});
        return;
    }

    // snapshot missing
    dir.access(io, snapshot_file_name, .{}) catch |err| switch (err) {
        error.FileNotFound => {
            std.debug.print(" [[SNAPSHOT MISSING]]\n", .{});
            return error.MissingSnapshot;
        },
        else => return err,
    };

    if (code == 1) {
        std.debug.print(" [[NOT EQUAL]]\n", .{});
        std.debug.print("{s}", .{diff.stdout});
        return error.SnapshotMistmatch;
    }
    if (code == 0) {
        std.debug.print(" [[EQUAL]]\n", .{});
        return;
    }
    return error.CommandFailed;
}

pub fn main(init: std.process.Init) !void {
    const arena = init.arena;
    const args = try init.minimal.args.toSlice(arena.allocator());
    // std.debug.assert(2 >= args.len and args.len <= 4);
    const io = init.io;
    const alloc = init.gpa;

    var should_regen_snapshot = false;
    var target: Target = .{
        .host = .X86,
        .device = .gfx1103,
    };
    for (args[1..]) |arg| {
        if (std.mem.eql(u8, arg, "--regen")) should_regen_snapshot = true;
        if (std.mem.eql(u8, arg, "--host=arm")) target.host = .ARM;
        if (std.mem.eql(u8, arg, "--host=x86")) target.host = .X86;
        if (std.mem.eql(u8, arg, "--device=host")) target.device = .host;
    }
    const compiler_path = args[1];

    const dir = try std.Io.Dir.cwd().openDir(io, "tst/python", .{ .iterate = true });
    defer dir.close(io);

    var walker = try dir.walk(alloc);
    defer walker.deinit();

    while (try walker.next(io)) |entry| {
        if (entry.kind != .file) continue;
        const flattened_name = try std.mem.replaceOwned(
            u8,
            alloc,
            entry.path,
            std.fs.path.sep_str,
            "__",
        );
        defer alloc.free(flattened_name);
        const file_name = try std.fs.path.join(alloc, &.{ "tst/python", entry.path });
        defer alloc.free(file_name);
        run(compiler_path, file_name, flattened_name, should_regen_snapshot, target, alloc, io) catch {
            std.debug.print(" [[ERROR]]\n", .{});
        };
    }
}
