const std = @import("std");

const c = @import("frontend").python.c;
const walkAstWithRuntime = @import("frontend").run.walkAstWithRuntime;
const tuple = @import("frontend").tuple;
const lazy = @import("frontend").lazy;
const list = @import("frontend").list;
const print = @import("frontend").print;
const func = @import("frontend").func;
const inline_ = @import("frontend").inline_;
const runtime = @import("frontend").runtime;
const class = @import("frontend").class;
const gpu = @import("frontend").gpu;
const generics = @import("frontend").generics;
const repeat = @import("frontend").repeat;
const middle = @import("middle");
const backend = @import("backend");
const Assembler = @import("assembler").Assembler;
const Target = backend.Target;
const CompilationArifacts = backend.CompilationArifacts;
const metrics = @import("metrics.zig");
const loop = middle.loop;
const reg_alloc = middle.reg_alloc;
const reg_class = middle.reg_class;
const live = middle.live;
const igraph = middle.igraph;
const color = middle.color;
const RegisterFile = @import("common").register.RegisterFile;
const precolor = middle.precolor;
const phi = middle.phi;
const parallel_copies = middle.parallel_copies;
const copy = middle.copy;
const dead = middle.dead;
const peephole = middle.peephole;
const FunctionType = @import("common").ir.FunctionType;
const TimerMetrics = @import("common").timer.TimerMetrics;

const underline_code = "\x1b[4m";
const reset_code = "\x1b[0m";

pub fn main(init: std.process.Init) !void {
    c.Py_Initialize();
    defer _ = c.Py_FinalizeEx();

    const arena = init.arena;
    const args = try init.minimal.args.toSlice(arena.allocator());
    const io = init.io;

    // var alloc = arena.allocator();
    var debug_alloc = std.heap.DebugAllocator(.{}){};
    defer {
        const status = debug_alloc.deinit();
        if (status == .leak) {
            std.debug.print("leaks detected\n", .{});
        }
    }
    const alloc = debug_alloc.allocator();

    if (args.len < 3) {
        std.debug.print("usage: {s} <input file> <output asm> [--run]\n", .{args[0]});
        return;
    }

    const input_file = args[1];
    var should_run = false;
    var should_optim = false;
    var should_dump_ir = false;
    var should_dump_stats = false;
    var should_dump_time = false;
    var use_escape_codes = true;
    var std_lib_enabled = true;
    // default target
    var target: Target = .{
        .host = .X86,
        .device = .gfx1103,
    };
    for (args[2..]) |arg| {
        if (std.mem.eql(u8, arg, "--run")) should_run = true;
        if (std.mem.eql(u8, arg, "--optim")) should_optim = true;
        if (std.mem.eql(u8, arg, "--dump-ir")) should_dump_ir = true;
        if (std.mem.eql(u8, arg, "--dump-stats")) should_dump_stats = true;
        if (std.mem.eql(u8, arg, "--dump-time")) should_dump_time = true;
        if (std.mem.eql(u8, arg, "--omit-escape-codes")) use_escape_codes = false;
        if (std.mem.eql(u8, arg, "--no-stdlib")) std_lib_enabled = false;
        // allow caller to decide their platform
        if (std.mem.eql(u8, arg, "--host=arm")) target.host = .ARM;
        if (std.mem.eql(u8, arg, "--host=x86")) target.host = .X86;
        if (std.mem.eql(u8, arg, "--device=host")) target.device = .host;
        if (std.mem.eql(u8, arg, "--device=gfx1103")) target.device = .gfx1103;
    }

    // walk user program
    var ir_program = try walkAstWithRuntime(input_file, should_optim, use_escape_codes, std_lib_enabled, io, alloc);
    defer ir_program.deinit(alloc);

    // rewrite layer
    var timer = TimerMetrics.init();
    timer.begin(.frontend_total, io);
    try class.rewrite(&ir_program, alloc);
    try generics.rewrite(&ir_program, alloc);
    try inline_.rewrite(&ir_program, alloc);
    try repeat.rewrite(&ir_program, alloc);
    try generics.rewrite(&ir_program, alloc);
    generics.dropTemplates(&ir_program, alloc);
    try gpu.rewrite(&ir_program, alloc);
    try print.rewrite(&ir_program, alloc);
    try func.rewrite(&ir_program, alloc);
    try lazy.rewrite(&ir_program, alloc);
    try list.rewrite(&ir_program, alloc);
    try tuple.rewrite(&ir_program, alloc);

    if (std_lib_enabled) {
        try runtime.injectCleanup(&ir_program, alloc);
    }

    // phi cleanup
    try phi.eliminatePhi(&ir_program, alloc);

    const host_platform = target.host.getPlatform();
    try precolor.apply(&ir_program, host_platform.abi, alloc);
    try parallel_copies.lower(&ir_program, alloc);

    // run optimization passses
    if (should_optim) {
        // expose constants to peephole
        try copy.run(&ir_program, alloc);
        try peephole.run(&ir_program, alloc);
        // cleanup peephole optims
        try copy.run(&ir_program, alloc);
    }
    // dump ir after optim pass
    if (should_dump_ir) {
        std.debug.print("\n", .{});
        if (use_escape_codes) std.debug.print("{s}", .{underline_code});
        std.debug.print("post phi elimination:", .{});
        if (use_escape_codes) std.debug.print("{s}", .{reset_code});
        std.debug.print("\n", .{});
        try ir_program.print();
    }

    timer.begin(.middle_reg_class, io);
    var reg_classes = try reg_class.classify(ir_program, alloc);
    timer.finish(.middle_reg_class, io);
    defer reg_classes.deinit();
    timer.finish(.frontend_total, io);

    timer.begin(.middle_total, io);
    timer.begin(.middle_reg_alloc_build, io);
    var alloc_program = try reg_alloc.build(ir_program, &reg_classes, alloc);
    timer.finish(.middle_reg_alloc_build, io);
    defer alloc_program.deinit(alloc);

    timer.begin(.middle_liveness, io);
    try live.calculateLiveOut(&alloc_program, alloc);
    timer.finish(.middle_liveness, io);

    // run optimzation passes
    if (should_optim) {
        timer.begin(.middle_dead, io);
        try dead.run(&ir_program, &alloc_program, alloc);
        timer.finish(.middle_dead, io);
        alloc_program.deinit(alloc);
        reg_classes.deinit();

        timer.begin(.middle_reg_class, io);
        reg_classes = try reg_class.classify(ir_program, alloc);
        timer.finish(.middle_reg_class, io);

        timer.begin(.middle_reg_alloc_build, io);
        alloc_program = try reg_alloc.build(ir_program, &reg_classes, alloc);
        timer.finish(.middle_reg_alloc_build, io);
        timer.begin(.middle_liveness, io);
        try live.calculateLiveOut(&alloc_program, alloc);
        timer.finish(.middle_liveness, io);
    }

    // setup register specifics
    const register_files = host_platform.abi.registerFiles();
    // generate interference graph
    var host_colors = color.ColoredGraph.initEmpty(alloc);
    defer host_colors.deinit();
    var spill_rounds = std.EnumArray(FunctionType, usize).initFill(0);
    for (register_files) |register_file| {
        timer.begin(.middle_igraph, io);
        var graph = try igraph.createIgraph(alloc_program.lines, register_file, alloc);
        timer.finish(.middle_igraph, io);
        defer graph.deinit();
        var result = try loop.run(
            &ir_program,
            &graph,
            &alloc_program,
            register_file,
            should_optim,
            &timer,
            io,
            alloc,
        );
        defer result.graph.deinit();
        try host_colors.absorb(&result.graph);

        inline for (std.meta.tags(FunctionType)) |function_type| {
            spill_rounds.getPtr(function_type).* += result.spill_rounds.get(function_type);
        }
    }

    // dump colored graph
    if (should_dump_ir) {
        std.debug.print("\n", .{});
        if (use_escape_codes) std.debug.print("{s}", .{underline_code});
        std.debug.print("post register allocation:", .{});
        if (use_escape_codes) std.debug.print("{s}", .{reset_code});
        std.debug.print("\n", .{});
        try ir_program.print();
    }

    var device_colors = color.ColoredGraph.initEmpty(alloc);
    defer device_colors.deinit();
    switch (target.device) {
        .host => {},
        .gfx1103 => {
            const device_platform = target.device.getPlatform();

            for (device_platform.abi.registerFiles()) |register_file| {
                timer.begin(.middle_igraph, io);
                var graph = try igraph.createIgraph(alloc_program.lines, register_file, alloc);
                timer.finish(.middle_igraph, io);
                defer graph.deinit();
                var result = try loop.run(
                    &ir_program,
                    &graph,
                    &alloc_program,
                    register_file,
                    should_optim,
                    &timer,
                    io,
                    alloc,
                );
                defer result.graph.deinit();
                try device_colors.absorb(&result.graph);
            }
        },
    }
    timer.finish(.middle_total, io);

    timer.begin(.backend_total, io);
    timer.begin(.backend_codegen, io);
    var artifacts = try (backend.CompileRequest{
        .program = &ir_program,
        .host_colors = &host_colors,
        .device_colors = &device_colors,
        .target = target,
    }).compile(alloc);
    defer artifacts.deinit(alloc);
    timer.finish(.backend_codegen, io);

    if (should_dump_stats) {
        const stats = metrics.get(artifacts.host_asm, spill_rounds, target);
        stats.user.print(use_escape_codes);
        if (std_lib_enabled) stats.runtime.print(use_escape_codes);
    }

    timer.begin(.backend_write_asm, io);
    const output_file = "/tmp/host.s";
    try writeArtifact(output_file, artifacts.host_asm, io);
    if (artifacts.device_asm) |device_asm|
        try writeArtifact("/tmp/device.s", device_asm, io);
    timer.finish(.backend_write_asm, io);

    if (should_run) {
        timer.begin(.backend_assemble, io);
        if (artifacts.device_asm != null) {
            const device_asm_result = try runCommand(alloc, io, &.{
                "clang",
                "-target",
                "amdgcn-amd-amdhsa",
                "-mcpu=gfx1103",
                "-c",
                "/tmp/device.s",
                "-o",
                "/tmp/device.o",
            });
            defer alloc.free(device_asm_result.stdout);
            defer alloc.free(device_asm_result.stderr);

            const device_link_result = try runCommand(alloc, io, &.{
                "ld.lld",
                "-shared",
                "/tmp/device.o",
                "-o",
                "/tmp/device.co",
            });
            defer alloc.free(device_link_result.stdout);
            defer alloc.free(device_link_result.stderr);
        }
        const dir = std.fs.path.dirname(output_file) orelse ".";
        const stem = std.fs.path.stem(output_file);
        const obj_name = try std.fmt.allocPrint(alloc, "{s}.o", .{stem});
        defer alloc.free(obj_name);
        const obj_file = try std.fs.path.join(alloc, &.{ dir, obj_name });
        defer alloc.free(obj_file);

        const hsa_runtime_path = init.environ_map.get("HSA_RUNTIME_PATH");
        try Assembler.run(.clang, .{
            .input_file = output_file,
            .output_file = obj_file,
            .target = target,
            .hsa_runtime_path = hsa_runtime_path,
        }, io, alloc);
        timer.finish(.backend_assemble, io);

        // clang is doing all assembling
        // create /tmp/integration_out
        timer.begin(.backend_link, io);
        const clang_final_result = if (target.device == .host)
            try runCommand(alloc, io, &.{
                "clang",
                obj_file,
                "/tmp/malloc.o",
                // link against libm
                "-lm",
                "-o",
                "/tmp/integration_out",
            })
        else
            try runCommand(alloc, io, &.{
                "clang",
                obj_file,
                "/tmp/malloc.o",
                "/tmp/gpu.o",
                "-lhsa-runtime64",
                // link against libm
                "-lm",
                "-o",
                "/tmp/integration_out",
            });
        defer alloc.free(clang_final_result.stdout);
        defer alloc.free(clang_final_result.stderr);
        timer.finish(.backend_link, io);
        // run!
        timer.begin(.backend_execute, io);
        const run_result = try runCommand(alloc, io, &.{"/tmp/integration_out"});
        timer.finish(.backend_execute, io);
        defer alloc.free(run_result.stdout);
        defer alloc.free(run_result.stderr);

        std.debug.print("\n", .{});
        if (use_escape_codes) std.debug.print("{s}", .{underline_code});
        std.debug.print("actual output:", .{});
        if (use_escape_codes) std.debug.print("{s}", .{reset_code});
        std.debug.print("\n", .{});
        std.debug.print("{s}", .{run_result.stdout});
    }
    timer.finish(.backend_total, io);
    if (should_dump_time) {
        timer.print(use_escape_codes);
    }
}

fn writeArtifact(output_file: []const u8, contents: []const u8, io: std.Io) !void {
    const file = try std.Io.Dir.createFileAbsolute(io, output_file, .{});
    var file_buf: [1028]u8 = undefined;
    var file_writer: std.Io.File.Writer = .init(file, io, &file_buf);

    defer file.close(io);
    try file_writer.interface.writeAll(contents);
    try file_writer.interface.flush();
}

pub fn runCommand(alloc: std.mem.Allocator, io: std.Io, argv: []const []const u8) !std.process.RunResult {
    const result = try std.process.run(alloc, io, .{
        .argv = argv,
        .stdout_limit = .limited(1 << 20),
        .stderr_limit = .limited(1 << 20),
    });
    if (result.term != .exited or result.term.exited != 0) {
        std.debug.print(
            "command failed: {s}\nterm: {any}\nstdout:\n{s}\nstderr:\n{s}\n",
            .{ argv[0], result.term, result.stdout, result.stderr },
        );

        alloc.free(result.stdout);
        alloc.free(result.stderr);
        return error.CommandFailed;
    }
    return result;
}
