const std = @import("std");
const ArrayList = @import("std").ArrayList;
const debugPrint = @import("std").debug.print;
const LocalInfo = @import("ir.zig").LocalInfo;
const Operand = @import("alloc.zig").Operand;
const ConstValue = @import("ir.zig").ConstValue;
const ValueRef = @import("ir.zig").ValueRef;
const BinOp = @import("ir.zig").BinOp;
const BlockId = @import("ir.zig").BlockId;
const CmpOp = @import("ir.zig").CmpOp;
const UnaryOp = @import("ir.zig").UnaryOp;
const SeenValue = @import("ir.zig").SeenValue;
const SeenValuePtr = @import("ir.zig").SeenValuePtr;
const TypeInfo = @import("types.zig").TypeInfo;
const TypedOperand = @import("alloc.zig").TypedOperand;

pub const Instruction = union(enum) {
    store_local: struct {
        local: LocalInfo,
        src: TypedOperand,
    },
    load_local: struct {
        dst: TypedOperand,
        local: LocalInfo,
    },
    binop: struct {
        dst: TypedOperand,
        op: BinOp,
        lhs: TypedOperand,
        rhs: TypedOperand,
    },
    move: struct {
        dst: TypedOperand,
        src: ValueRef,
    },
    unaryop: struct {
        dst: TypedOperand,
        op: UnaryOp,
        src: TypedOperand,
    },
    compare: struct {
        dst: TypedOperand,
        lhs: TypedOperand,
        op: CmpOp,
        rhs: TypedOperand,
    },
    jump: struct {
        target: BlockId,
    },
    branch: struct {
        condition: TypedOperand,
        then_block: BlockId,
        else_block: BlockId,
    },
    // dst <- *(src + offset)
    load_offset: struct {
        dst: TypedOperand,
        src: TypedOperand,
        offset: ValueRef,
    },
    // *(dst + offset) <- src
    store_offset: struct {
        dst: TypedOperand,
        /// offset in bytes
        offset: ValueRef,
        src: TypedOperand,
    },
    stack_alloc: struct {
        dst: TypedOperand,
        bytes: usize,
    },
    select: struct {
        dst: TypedOperand,
        condition: TypedOperand,
        if_value: ValueRef,
        else_value: ValueRef,
    },
    cast: struct {
        dst: TypedOperand,
        dst_target_type: TypeInfo,
        src: TypedOperand,
    },
    unkown,

    pub fn printFn(self: @This()) !void {
        switch (self) {
            .binop => |binop| {
                binop.dst.operand.print();
                debugPrint(" <- {s} ", .{@tagName(binop.op)});
                binop.lhs.operand.print();
                debugPrint(", ", .{});
                binop.rhs.operand.print();
                debugPrint("\n", .{});
            },
            .store_local => |sl| {
                debugPrint("\"{s}\" <- ", .{sl.local.name});
                sl.src.operand.print();
                debugPrint("\n", .{});
            },
            .store_offset => |so| {
                debugPrint("*(", .{});
                so.dst.operand.print();
                debugPrint(" + ", .{});
                so.offset.print();
                debugPrint(") <- ", .{});
                so.src.operand.print();
                debugPrint("\n", .{});
            },
            .load_offset => |lo| {
                lo.dst.operand.print();
                debugPrint(" <- *(", .{});
                lo.src.operand.print();
                debugPrint(" + ", .{});
                lo.offset.print();
                debugPrint(")\n", .{});
            },
            .stack_alloc => |sa| {
                sa.dst.operand.print();
                debugPrint(" <- stack_alloc {d} bytes\n", .{sa.bytes});
            },
            .load_local => |ll| {
                ll.dst.operand.print();
                debugPrint(" <- \"{s}\"\n", .{ll.local.name});
            },
            .unaryop => |uop| {
                uop.dst.operand.print();
                debugPrint(" <- {s} ", .{@tagName(uop.op)});
                uop.src.operand.print();
                debugPrint("\n", .{});
            },
            .move => |m| {
                m.dst.operand.print();
                debugPrint(" <- ", .{});
                m.src.print();
                debugPrint("\n", .{});
            },
            .compare => |c| {
                c.dst.operand.print();
                debugPrint(" <- ", .{});
                c.lhs.operand.print();
                debugPrint(" {s} ", .{c.op.symbol()});
                c.rhs.operand.print();
                debugPrint("\n", .{});
            },
            .jump => |j| {
                debugPrint("jump block{d}\n", .{j.target});
            },
            .branch => |b| {
                b.condition.operand.print();
                debugPrint(" ? jump block{d} : jump block{d}\n", .{ b.then_block, b.else_block });
            },
            .select => |s| {
                s.dst.operand.print();
                debugPrint(" <- ", .{});
                s.condition.operand.print();
                debugPrint(" ? ", .{});
                s.if_value.print();
                debugPrint(" : ", .{});
                s.else_value.print();
                debugPrint("\n", .{});
            },
            .cast => |c| {
                c.dst.operand.print();
                debugPrint(" <- ({s})", .{@tagName(c.dst_target_type)});
                c.src.operand.print();
                debugPrint("\n", .{});
            },
            else => |term| {
                std.debug.panic("ir instruction not impl: {s}", .{@tagName(term)});
                return error.NotImplemented;
            },
        }
    }

    pub fn replaceUses(self: *@This(), old: Operand, new: Operand) void {
        switch (self.*) {
            .store_local => |*sl| {
                if (sl.src.operand.equal(old)) sl.src.operand = new;
            },
            .store_offset => |*so| {
                if (so.dst.operand.equal(old)) so.dst.operand = new;
                if (so.src.operand.equal(old)) so.src.operand = new;
                switch (so.offset) {
                    .top => |*top| {
                        if (top.operand.equal(old)) top.operand = new;
                    },
                    .constant => {},
                }
            },
            .load_offset => |*lo| {
                if (lo.src.operand.equal(old)) lo.src.operand = new;
                switch (lo.offset) {
                    .top => |*top| {
                        if (top.operand.equal(old)) top.operand = new;
                    },
                    .constant => {},
                }
            },
            .binop => |*bop| {
                if (bop.lhs.operand.equal(old)) {
                    bop.lhs.operand = new;
                }
                if (bop.rhs.operand.equal(old)) {
                    bop.rhs.operand = new;
                }
            },
            .move => |*mov| {
                switch (mov.src) {
                    .top => |*top| {
                        if (top.operand.equal(old)) top.operand = new;
                    },
                    .constant => {},
                }
            },
            .compare => |*c| {
                if (c.lhs.operand.equal(old)) c.lhs.operand = new;
                if (c.rhs.operand.equal(old)) c.rhs.operand = new;
            },
            .select => |*s| {
                if (s.condition.operand.equal(old)) s.condition.operand = new;
                switch (s.if_value) {
                    .top => |*top| {
                        if (top.operand.equal(old)) {
                            top.operand = new;
                        }
                    },
                    else => {},
                }
                switch (s.else_value) {
                    .top => |*top| {
                        if (top.operand.equal(old)) {
                            top.operand = new;
                        }
                    },
                    else => {},
                }
            },
            .cast => |*c| {
                if (c.src.operand.equal(old)) c.src.operand = new;
            },
            .unaryop => |*uop| {
                if (uop.src.operand.equal(old)) uop.src.operand = new;
            },
            else => |e| {
                debugPrint("uses cant handle {s}\n", .{@tagName(e)});
                unreachable;
            },
        }
    }

    pub fn replaceDefines(self: *@This(), old: Operand, new: Operand) void {
        switch (self.*) {
            .binop => |*bop| {
                if (bop.dst.operand.equal(old)) bop.dst.operand = new;
            },
            .move => |*mov| {
                if (mov.dst.operand.equal(old)) mov.dst.operand = new;
            },
            .compare => |*c| {
                if (c.dst.operand.equal(old)) c.dst.operand = new;
            },
            .load_offset => |*lo| {
                if (lo.dst.operand.equal(old)) lo.dst.operand = new;
            },
            .select => |*s| {
                if (s.dst.operand.equal(old)) s.dst.operand = new;
            },
            .unaryop => |*uop| {
                if (uop.dst.operand.equal(old)) uop.dst.operand = new;
            },
            .cast => |*c| {
                if (c.dst.operand.equal(old)) c.dst.operand = new;
            },
            else => |e| {
                debugPrint("defines cant handle {s}\n", .{@tagName(e)});
                unreachable;
            },
        }
    }

    pub fn getDefines(instruction: Instruction) !SeenValue {
        var addressable_instruction = instruction;
        return addressable_instruction.getDefinePtrs();
    }

    /// are we generating a new temp for reg coloring
    pub fn getDefinePtrs(instruction: *Instruction) ?SeenValuePtr {
        return switch (instruction.*) {
            .store_local => |*sl| .{ .local = &sl.local.id },
            .load_local => |*ll| .{ .top = &ll.dst },
            .binop => |*bop| .{ .top = &bop.dst },
            .move => |*m| .{ .top = &m.dst },
            .unaryop => |*uop| .{ .top = &uop.dst },
            .compare => |*c| .{ .top = &c.dst },
            .store_offset => null,
            .load_offset => |*lo| .{ .top = &lo.dst },
            .stack_alloc => |*so| .{ .top = &so.dst },
            .select => |*s| .{ .top = &s.dst },
            .branch => null,
            .jump => null,
            .cast => |*c| .{ .top = &c.dst },
            else => |e| {
                std.debug.print("getDefines does not handle {s}\n", .{@tagName(e)});
                unreachable;
            },
        };
    }

    pub fn getUses(instruction: Instruction, alloc: std.mem.Allocator) !ArrayList(SeenValue) {
        var addressable_instruction = instruction;
        var uses = try addressable_instruction.getUsePtrs(alloc);
        defer uses.deinit(alloc);

        var result: ArrayList(SeenValue) = .empty;
        errdefer result.deinit(alloc);

        for (uses.items) |use| {
            try result.append(alloc, use.value());
        }
        return result;
    }

    pub fn getUsePtrs(instruction: *Instruction, alloc: std.mem.Allocator) !ArrayList(SeenValuePtr) {
        var res: ArrayList(SeenValuePtr) = .empty;
        errdefer res.deinit(alloc);

        switch (instruction.*) {
            .store_local => |*sl| {
                try res.append(alloc, .{ .top = &sl.src });
            },
            .store_offset => |*so| {
                try res.append(alloc, .{ .top = &so.dst });
                switch (so.offset) {
                    .top => |*top| {
                        try res.append(alloc, .{ .top = top });
                    },
                    else => {},
                }
                try res.append(alloc, .{ .top = &so.src });
            },
            .load_offset => |*lo| {
                switch (lo.offset) {
                    .top => |*top| {
                        try res.append(alloc, .{ .top = top });
                    },
                    else => {},
                }
                try res.append(alloc, .{ .top = &lo.src });
            },
            .stack_alloc => {},
            .load_local => |*ll| {
                try res.append(alloc, .{ .local = &ll.local.id });
            },
            .binop => |*bop| {
                try res.append(alloc, .{ .top = &bop.lhs });
                try res.append(alloc, .{ .top = &bop.rhs });
            },
            .move => |*m| {
                switch (m.src) {
                    .top => |*top| {
                        try res.append(alloc, .{ .top = top });
                    },
                    .constant => {},
                }
            },
            .unaryop => |*uop| {
                try res.append(alloc, .{ .top = &uop.src });
            },
            .compare => |*c| {
                try res.append(alloc, .{ .top = &c.lhs });
                try res.append(alloc, .{ .top = &c.rhs });
            },
            .branch => |*b| {
                try res.append(alloc, .{ .top = &b.condition });
            },
            .select => |*s| {
                try res.append(alloc, .{ .top = &s.condition });
                switch (s.if_value) {
                    .top => |*top| {
                        try res.append(alloc, .{ .top = top });
                    },
                    else => {},
                }
                switch (s.else_value) {
                    .top => |*top| {
                        try res.append(alloc, .{ .top = top });
                    },
                    else => {},
                }
            },
            .jump => {},
            .cast => |*c| {
                try res.append(alloc, .{ .top = &c.src });
            },
            else => |e| {
                std.debug.print("getUses doesn't handle {s}\n", .{@tagName(e)});
                return error.NotImpl;
            },
        }
        return res;
    }

    pub fn deinit(self: *@This(), alloc: std.mem.Allocator) void {
        switch (self.*) {
            .binop => |bop| {
                bop.dst.deinit(alloc);
                bop.lhs.deinit(alloc);
                bop.rhs.deinit(alloc);
            },
            .compare => |c| {
                c.dst.deinit(alloc);
                c.lhs.deinit(alloc);
                c.rhs.deinit(alloc);
            },
            .unaryop => |u| {
                u.dst.deinit(alloc);
                u.src.deinit(alloc);
            },
            .branch => |b| {
                b.condition.deinit(alloc);
            },
            .cast => |c| {
                c.dst.deinit(alloc);
                c.dst_target_type.deinit(alloc);
                c.src.deinit(alloc);
            },
            .select => |s| {
                s.dst.deinit(alloc);
                s.condition.deinit(alloc);
                s.if_value.deinit(alloc);
                s.else_value.deinit(alloc);
            },
            .move => |m| {
                m.dst.type.deinit(alloc);
                m.src.deinit(alloc);
            },
            .store_local => |sl| {
                alloc.free(sl.local.name);
                sl.local.type.deinit(alloc);
                sl.src.type.deinit(alloc);
            },
            .load_local => |ll| {
                alloc.free(ll.local.name);
                ll.dst.deinit(alloc);
            },
            .load_offset => |lo| {
                lo.dst.type.deinit(alloc);
                lo.src.type.deinit(alloc);
                switch (lo.offset) {
                    .top => |top| {
                        top.type.deinit(alloc);
                    },
                    .constant => {},
                }
            },
            .store_offset => |so| {
                so.dst.type.deinit(alloc);
                so.src.type.deinit(alloc);
                so.offset.deinit(alloc);
            },
            .stack_alloc => |so| {
                so.dst.type.deinit(alloc);
            },
            else => {},
        }
    }

    pub fn clone(self: *@This(), alloc: std.mem.Allocator) !@This() {
        return switch (self.*) {
            .store_local => |sl| .{ .store_local = .{
                .local = try sl.local.clone(alloc),
                .src = try sl.src.clone(alloc),
            } },
            .store_offset => |so| .{ .store_offset = .{
                .dst = try so.dst.clone(alloc),
                .offset = try so.offset.clone(alloc),
                .src = try so.src.clone(alloc),
            } },
            .load_offset => |lo| .{ .load_offset = .{
                .dst = try lo.dst.clone(alloc),
                .src = try lo.src.clone(alloc),
                .offset = try lo.offset.clone(alloc),
            } },
            .move => |m| .{ .move = .{
                .dst = try m.dst.clone(alloc),
                .src = try m.src.clone(alloc),
            } },
            .binop => |bop| .{ .binop = .{
                .dst = try bop.dst.clone(alloc),
                .lhs = try bop.lhs.clone(alloc),
                .op = bop.op,
                .rhs = try bop.rhs.clone(alloc),
            } },
            .jump => |j| .{ .jump = .{
                .target = j.target,
            } },
            .compare => |c| .{ .compare = .{
                .dst = try c.dst.clone(alloc),
                .lhs = try c.lhs.clone(alloc),
                .op = c.op,
                .rhs = try c.rhs.clone(alloc),
            } },
            .branch => |b| .{ .branch = .{
                .condition = try b.condition.clone(alloc),
                .then_block = b.then_block,
                .else_block = b.else_block,
            } },
            .load_local => |ll| .{ .load_local = .{
                .dst = try ll.dst.clone(alloc),
                .local = try ll.local.clone(alloc),
            } },
            .select => |s| .{ .select = .{
                .dst = try s.dst.clone(alloc),
                .condition = try s.condition.clone(alloc),
                .if_value = try s.if_value.clone(alloc),
                .else_value = try s.else_value.clone(alloc),
            } },
            .cast => |c| .{ .cast = .{
                .dst = try c.dst.clone(alloc),
                .dst_target_type = try c.dst_target_type.clone(alloc),
                .src = try c.src.clone(alloc),
            } },
            .unaryop => |uo| .{ .unaryop = .{
                .dst = try uo.dst.clone(alloc),
                .op = uo.op,
                .src = try uo.src.clone(alloc),
            } },
            else => |e| {
                std.debug.print("cant handle {s}\n", .{@tagName(e)});
                return error.NotImpl;
            },
        };
    }
};
