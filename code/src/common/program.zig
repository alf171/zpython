const std = @import("std");
const debugPrint = std.debug.print;
const ArrayList = std.ArrayList;
const BasicBlock = @import("ir.zig").BasicBlock;
const ClassInfo = @import("ir.zig").ClassInfo;
const Function = @import("ir.zig").Function;
const FunctionType = @import("ir.zig").FunctionType;
const Param = @import("ir.zig").Param;
const TypeParam = @import("ir.zig").TypeParam;
const Operand = @import("alloc.zig").Operand;
const TypeInfo = @import("types.zig").TypeInfo;
const ModuleId = @import("module.zig").ModuleId;

pub const Program = struct {
    main: Function,
    functions: ArrayList(Function),
    classes: ArrayList(ClassInfo),

    pub fn init(module_id: ModuleId, alloc: std.mem.Allocator) !Program {
        var blocks: ArrayList(BasicBlock) = .empty;
        const entry: BasicBlock = .init(0);
        try blocks.append(alloc, entry);

        return .{
            .main = .{
                .name = try alloc.dupe(u8, "main"),
                .id = 0,
                .module_id = module_id,
                .blocks = blocks,
                .entry_block = 0,
                .params = try alloc.alloc(Param, 0),
                .type_params = try alloc.alloc(TypeParam, 0),
                .return_type = .i64,
                .next_temp = 0,
                .next_mem = 0,
                .origin = .user,
                .kind = .host,
                .is_inline = false,
                .value_to_type = std.AutoHashMap(Operand, TypeInfo).init(alloc),
            },
            .functions = .empty,
            .classes = .empty,
        };
    }

    pub fn deinit(self: *@This(), alloc: std.mem.Allocator) void {
        self.main.deinit(alloc);
        for (self.functions.items) |*func| {
            func.deinit(alloc);
        }
        self.functions.deinit(alloc);
        for (self.classes.items) |*class| {
            class.deinit(alloc);
        }
        self.classes.deinit(alloc);
    }

    pub fn print(self: @This()) !void {
        for (self.functions.items) |function| {
            debugPrint("\n{s} -> {s}:\n", .{ function.name, @tagName(function.return_type) });
            for (function.blocks.items) |block| {
                debugPrint("block{d}:\n", .{block.id});

                for (block.instructions.items) |*instruction| {
                    debugPrint("  ", .{});
                    try instruction.printFn();
                }
            }
        }
        debugPrint("\n{s} -> {s}:\n", .{ self.main.name, @tagName(self.main.return_type) });
        for (self.main.blocks.items) |block| {
            debugPrint("block{d}:\n", .{block.id});

            for (block.instructions.items) |*instruction| {
                debugPrint("  ", .{});
                try instruction.printFn();
            }
        }
    }
};
