const std = @import("std");
const ArrayList = std.ArrayList;
const c = @import("python.zig").c;
const StmtKind = @import("python.zig").StmtKind;
const getStmtKind = @import("python.zig").getStmtKind;
const getPyType = @import("python.zig").getPyType;
const printAstDump = @import("python.zig").printAstDump;

const Function = @import("common").ir.Function;
const FunctionKind = @import("common").ir.FunctionKind;
const ConstValue = @import("common").ir.ConstValue;
const ParsedConstant = @import("common").ir.ParsedConstant;
const BasicBlock = @import("common").ir.BasicBlock;
const TypeBindings = @import("common").types.TypeBindings;
const TypeInfo = @import("common").types.TypeInfo;
const Operand = @import("common").alloc.Operand;
const TypedOperand = @import("common").alloc.TypedOperand;
const ValueRef = @import("common").ir.ValueRef;
const Param = @import("common").ir.Param;
const TypeParam = @import("common").ir.TypeParam;
const LocalInfo = @import("common").ir.LocalInfo;
const ClassId = @import("common").ir.ClassId;
const ClassInfo = @import("common").ir.ClassInfo;
const ClassInstance = @import("common").types.ClassInstance;
const Field = @import("common").ir.Field;
const Method = @import("common").ir.Method;
const LocalId = @import("common").ir.LocalId;
const Instruction = @import("common").mir.Instruction;
const BinOp = @import("common").ir.BinOp;
const CmpOp = @import("common").ir.CmpOp;
const Program = @import("common").program.Program;
const PhiInput = @import("common").mir.PhiInput;
const UnaryOp = @import("common").ir.UnaryOp;

const IrBuilder = @import("ir_builder.zig").IrBuilder;
const LocalValues = @import("ir_builder.zig").LocalValues;

const LoopBody = @import("loop.zig").LoopBody;
const walkLoop = @import("loop.zig").walkLoop;
const LoopCarry = @import("loop.zig").LoopCarry;

const PyObject = c.PyObject;

const ExprKind = enum { BinOp, UnaryOp, Compare, Constant, Name, Call, List, Tuple, Subscript, IfExp, Attribute, Unknown };

const BuiltinCall = enum { Print, Write, Range, Len, Int, I32, Float, GlobalIdx, Max, Exp, Exp2, Type };

const SubscriberTypes = union(enum) {
    list,
    tuple,
    callable,
    instance: ClassId,
};

const RangeBounds = struct {
    start: TypedOperand,
    end: TypedOperand,
};

pub fn walkAstIntoBuilder(obj: ?*c.PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) !void {
    if (obj == null) return;

    const body = c.PyObject_GetAttrString(obj, "body");
    std.debug.assert(body != null);

    try walkStmtList(body, irBuilder, alloc);
}

pub fn walkStmtList(stmts: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) anyerror!void {
    const n = c.PyList_Size(stmts);
    var i: isize = 0;

    while (i < n) : (i += 1) {
        const raw_stmt = c.PyList_GetItem(stmts, i);
        try walkStmt(raw_stmt, irBuilder, alloc);
    }
}

pub fn walkStmt(raw_stmt: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) anyerror!void {
    const stmt = getStmtKind(raw_stmt);
    switch (stmt) {
        .Assign => try walkAssignment(raw_stmt, irBuilder, alloc),
        .AnnotatedAssign => try walkAnnotatedAssignment(raw_stmt, irBuilder, alloc),
        .Expr => {
            const value = c.PyObject_GetAttrString(raw_stmt, "value");
            const expr = try walkExpr(value, irBuilder, null, alloc);
            expr.deinit(alloc);
        },
        .If => try walkIf(raw_stmt, irBuilder, alloc),
        .While => try walkWhile(raw_stmt, irBuilder, alloc),
        .For => try walkFor(raw_stmt, irBuilder, alloc),
        .FuncDef => try walkFuncDef(raw_stmt, irBuilder, null, alloc),
        .Return => try walkReturn(raw_stmt, irBuilder, alloc),
        .Pass => {},
        // imports handled in `module.zig`
        .Import, .ImportFrom => {},
        .AugAssign => try walkAugAssignment(raw_stmt, irBuilder, alloc),
        .ClassDef => try walkClassDef(raw_stmt, irBuilder, alloc),
        else => {
            std.debug.print("unsupported statement type: {s}: ", .{getPyType(raw_stmt)});
            printAstDump(raw_stmt);
            return error.UnsupportedStatement;
        },
    }
}

// ClassDef(name='Car', body=[FunctionDef(name='__init__', args=arguments(...), body=[Assign(targets=[Attribute(value=Name(id='self', ctx=Load()), attr='name', ctx=Store())], value=Name(id='name', ctx=Load())), Assign(targets=[Attribute(value=Name(id='self', ctx=Load()), attr='speed', ctx=Store())], value=Name(id='speed', ctx=Load()))], returns=Constant(value=None)), FunctionDef(name='print_speed', args=arguments(args=[arg(arg='self')]), body=[...], returns=Constant(value=None))])
fn walkClassDef(stmt: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) !void {
    const name_obj = c.PyObject_GetAttrString(stmt, "name");
    std.debug.assert(name_obj != null);
    const raw_name = c.PyUnicode_AsUTF8(name_obj);
    std.debug.assert(raw_name != null);
    const name = std.mem.span(raw_name);

    const id: ClassId = irBuilder.nextClassIdx();
    const class_type_params = try parseTypeParams(stmt, 0, alloc);

    try irBuilder.program.classes.append(
        alloc,
        try ClassInfo.init(id, name, class_type_params, alloc),
    );
    const body_objs = c.PyObject_GetAttrString(stmt, "body");
    std.debug.assert(body_objs != null);
    const class = irBuilder.getClass(id);
    for (0..@intCast(c.PyList_Size(body_objs))) |i| {
        const body_obj = c.PyList_GetItem(body_objs, @intCast(i));
        std.debug.assert(body_obj != null);
        switch (getStmtKind(body_obj)) {
            .FuncDef => {
                const method_name_obj = c.PyObject_GetAttrString(body_obj, "name");
                std.debug.assert(method_name_obj != null);
                const raw_method_name = c.PyUnicode_AsUTF8(method_name_obj);
                std.debug.assert(raw_method_name != null);
                const method_name = std.mem.span(raw_method_name);

                try walkFuncDef(body_obj, irBuilder, id, alloc);
                // previous call appended a function
                const function_idx = irBuilder.nextFunctionId() - 2;
                const function = &irBuilder.program.functions.items[function_idx];

                try class.methods.append(alloc, .{
                    .name = try alloc.dupe(u8, method_name),
                    .function_name = try alloc.dupe(u8, function.name),
                    .function_id = function.id,
                    .is_static = try hasDecorator(body_obj, "staticmethod"),
                });
            },
            else => return error.NotImpl,
        }
    }
}

fn walkAugAssignment(stmt: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) !void {
    const lhs = c.PyObject_GetAttrString(stmt, "target");
    std.debug.assert(lhs != null);
    const lhs_value = try walkExpr(lhs, irBuilder, null, alloc);

    const rhs = c.PyObject_GetAttrString(stmt, "value");
    const rhs_value = try walkExpr(rhs, irBuilder, null, alloc);

    const result: TypedOperand = .{
        .operand = irBuilder.nextTemp(),
        .type = lhs_value.type,
    };
    try irBuilder.emit(.{ .lir = .{ .binop = .{
        .dst = result,
        .lhs = lhs_value,
        .op = try getBinOp(stmt),
        .rhs = rhs_value,
    } } }, alloc);
    try storeAssignmentTarget(lhs, result, irBuilder, alloc);
}

fn walkAssignment(stmt: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) !void {
    const targets = c.PyObject_GetAttrString(stmt, "targets");
    std.debug.assert(targets != null);

    const lhs = c.PyList_GetItem(targets, 0);
    std.debug.assert(lhs != null);

    const rhs = c.PyObject_GetAttrString(stmt, "value");
    const rhs_value = try walkExpr(rhs, irBuilder, null, alloc);
    try storeAssignmentTarget(lhs, rhs_value, irBuilder, alloc);
}

fn storeAssignmentTarget(lhs: *PyObject, rhs_value: TypedOperand, irBuilder: *IrBuilder, alloc: std.mem.Allocator) !void {
    const expr = getExprKind(lhs);
    switch (expr) {
        // Assign(targets=[Name(id='x', ctx=Store())], value=Constant(value=3))
        .Name => {
            const id_obj = c.PyObject_GetAttrString(lhs, "id");
            std.debug.assert(id_obj != null);
            const id = c.PyUnicode_AsUTF8(id_obj);

            const local = try irBuilder.getOrCreateLocal(std.mem.span(id), null, alloc);
            try irBuilder.putLocalValues(local, rhs_value, alloc);
            try irBuilder.emit(.{ .lir = .{ .store_local = .{
                .local = .{
                    .id = local,
                    .name = try alloc.dupe(u8, std.mem.span(id)),
                    .type = try rhs_value.type.clone(alloc),
                },
                .src = try rhs_value.clone(alloc),
            } } }, alloc);
        },
        // Assign(targets=[Subscript(value=Name(id='items', ctx=Load()), slice=Constant(value=3), ctx=Store())], value=Constant(value=0))
        .Subscript => {
            const slice_obj = c.PyObject_GetAttrString(lhs, "slice");
            std.debug.assert(slice_obj != null);
            const slice = try walkExpr(slice_obj, irBuilder, null, alloc);
            defer slice.deinit(alloc);
            const value_obj = c.PyObject_GetAttrString(lhs, "value");
            std.debug.assert(value_obj != null);
            const container = try walkExpr(value_obj, irBuilder, null, alloc);
            defer container.deinit(alloc);

            switch (container.type) {
                .list => {
                    try irBuilder.emit(.{ .subscript_store = .{
                        .target = try container.clone(alloc),
                        .index = try slice.clone(alloc),
                        .src = .{ .top = rhs_value },
                    } }, alloc);
                },
                .instance => {
                    try irBuilder.emit(.{ .subscript_store = .{
                        .target = try container.clone(alloc),
                        .index = try slice.clone(alloc),
                        .src = .{ .top = rhs_value },
                    } }, alloc);
                },
                else => |e| {
                    std.debug.print("cant handle {s}\n", .{@tagName(e)});
                    return error.UnexpectedType;
                },
            }
        },
        // Assign(targets=[Tuple(elts=[Name(id='x', ctx=Store()), Name(id='y', ctx=Store())], ctx=Store())], value=Call(func=Name(id='foobar', ctx=Load()), args=[Constant(value=1), Constant(value=2)]))
        .Tuple => {
            const elts = c.PyObject_GetAttrString(lhs, "elts");
            std.debug.assert(elts != null);
            for (0..@intCast(c.PyList_Size(elts))) |i| {
                const elt = c.PyList_GetItem(elts, @intCast(i));
                std.debug.assert(elt != null);
                if (getExprKind(elt) != .Name) return error.UnsupportedTarget;
                const index: TypedOperand = .{
                    .operand = irBuilder.nextTemp(),
                    .type = .i64,
                };
                try irBuilder.emit(.{ .lir = .{ .move = .{
                    .dst = index,
                    .src = .{ .constant = .{ .i64 = @intCast(i) } },
                } } }, alloc);

                const elem_type = switch (rhs_value.type) {
                    .tuple => |tuple| tuple.elements[i],
                    else => return error.ExpectTuple,
                };
                const elem_dst: TypedOperand = .{
                    .operand = irBuilder.nextTemp(),
                    .type = elem_type,
                };

                try irBuilder.emit(.{ .subscript = .{
                    .dst = elem_dst,
                    .src = rhs_value,
                    .index = index,
                } }, alloc);

                const id_obj = c.PyObject_GetAttrString(elt, "id");
                std.debug.assert(id_obj != null);
                const id = c.PyUnicode_AsUTF8(id_obj);

                const local = try irBuilder.getOrCreateLocal(std.mem.span(id), null, alloc);

                try irBuilder.putLocalValues(
                    local,
                    .{
                        .operand = elem_dst.operand,
                        .type = elem_type,
                    },
                    alloc,
                );
                try irBuilder.emit(.{ .lir = .{ .store_local = .{
                    .local = .{
                        .id = local,
                        .name = try alloc.dupe(u8, std.mem.span(id)),
                        .type = try elem_type.clone(alloc),
                    },
                    .src = elem_dst,
                } } }, alloc);
            }
        },
        // Attribute(value=Name(id='self', ctx=Load()), attr='name', ctx=Store())
        .Attribute => {
            const instance_obj = c.PyObject_GetAttrString(lhs, "value");
            std.debug.assert(instance_obj != null);
            const attribute_obj = c.PyObject_GetAttrString(lhs, "attr");
            std.debug.assert(attribute_obj != null);

            const raw_field_name = c.PyUnicode_AsUTF8(attribute_obj);
            const field_name: []const u8 = std.mem.span(raw_field_name);
            std.debug.assert(raw_field_name != null);

            const instance_expr = try walkExpr(instance_obj, irBuilder, null, alloc);
            errdefer instance_expr.deinit(alloc);
            const instance = switch (instance_expr.type) {
                .instance => |id| id,
                else => return error.ExpectedInstance,
            };
            const class = irBuilder.getClass(instance.class_id);
            const field_idx = class.findFieldIdx(std.mem.span(raw_field_name));
            var field: ?*Field = null;

            // first time self so define field
            if (field_idx == null) {
                try class.fields.append(alloc, .{
                    .name = try alloc.dupe(u8, field_name),
                    .type = try rhs_value.type.clone(alloc),
                    .offset = class.size,
                });
                class.size += try rhs_value.type.sizeOfType();
                field = &class.fields.items[class.fields.items.len - 1];
            } else {
                // reassignemnt scenario
                field = &class.fields.items[field_idx.?];
                var bindings: TypeBindings = .init(alloc);
                defer bindings.deinit(alloc);
                try field.?.type.unify(rhs_value.type, &bindings, alloc);
            }

            try irBuilder.emit(.{ .field_store = .{
                .instance = instance_expr,
                .offset = field.?.offset,
                .src = rhs_value,
            } }, alloc);
        },
        else => |e| {
            std.debug.print("cant handle {s}\n", .{@tagName(e)});
            return error.NotImpl;
        },
    }
}

// 1. AnnAssign(target=Name(id='a', ctx=Store()), annotation=..., value=Constant(value=5), simple=1)
// 2. AnnAssign(target=Name(id='a', ctx=Store()), annotation=..., value=List(elts=[Constant(value=1), Constant(value=2), Constant(value=3)], ctx=Load()), simple=1)
fn walkAnnotatedAssignment(stmt: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) !void {
    const target = c.PyObject_GetAttrString(stmt, "target");
    std.debug.assert(target != null);

    const annotation = c.PyObject_GetAttrString(stmt, "annotation");
    const annotation_type = try parseTypeAnnotation(annotation, irBuilder, alloc);
    defer annotation_type.deinit(alloc);
    const rhs = c.PyObject_GetAttrString(stmt, "value");
    const rhs_value = try walkExpr(rhs, irBuilder, annotation_type, alloc);

    switch (getExprKind(target)) {
        .Name => {
            const target_id_obj = c.PyObject_GetAttrString(target, "id");
            std.debug.assert(target_id_obj != null);
            const target_id = c.PyUnicode_AsUTF8(target_id_obj);
            const local = try irBuilder.getOrCreateLocal(
                std.mem.span(target_id),
                annotation_type,
                alloc,
            );
            try irBuilder.local_values.put(local, rhs_value);
            try irBuilder.emit(.{ .lir = .{ .store_local = .{
                .local = .{
                    .id = local,
                    .name = try alloc.dupe(u8, std.mem.span(target_id)),
                    .type = try annotation_type.clone(alloc),
                },
                .src = try rhs_value.clone(alloc),
            } } }, alloc);
        },
        .Attribute => try storeAssignmentTarget(target, rhs_value, irBuilder, alloc),
        else => return error.UnsupportedTarget,
    }
}

pub fn walkExpr(stmt: *PyObject, irBuilder: *IrBuilder, expected_type: ?TypeInfo, alloc: std.mem.Allocator) !TypedOperand {
    switch (getExprKind(stmt)) {
        .BinOp => {
            const left = c.PyObject_GetAttrString(stmt, "left");
            const right = c.PyObject_GetAttrString(stmt, "right");

            const op = try getBinOp(stmt);
            // order here will impact temp numbering
            const lhs = try walkExpr(left, irBuilder, null, alloc);
            const rhs = try walkExpr(right, irBuilder, null, alloc);

            if (lhs.type == .list and rhs.type == .i64) {
                // list_repeat owns clones of both operands, so release these
                // expression temporaries after building the instruction.
                defer lhs.deinit(alloc);
                defer rhs.deinit(alloc);
                const dst: TypedOperand = .{
                    .operand = irBuilder.nextTemp(),
                    .type = .{ .list = .{
                        .element = try (try lhs.type.list.element.clone(alloc)).toOwnedPointer(alloc),
                    } },
                };
                try irBuilder.emit(.{ .list_repeat = .{
                    .dst = dst,
                    .list = try lhs.clone(alloc),
                    .count = try rhs.clone(alloc),
                } }, alloc);
                return try dst.clone(alloc);
            }

            const result_type: TypeInfo = switch (lhs.type) {
                .instance => |instance| blk: {
                    const class = irBuilder.getClass(instance.class_id);
                    const func = try op.toClassBuiltin();
                    const method = class.findMethod(func) orelse {
                        return error.CantFindMethod;
                    };
                    const function = irBuilder.findFunction(method.function_name) orelse {
                        return error.CantFindFunction;
                    };
                    var bindings: TypeBindings = .init(alloc);
                    defer bindings.deinit(alloc);
                    const return_type = try bindings.inferReturnType(function, &.{ lhs, rhs }, alloc);
                    break :blk return_type;
                },
                else => blk: {
                    if (expected_type) |t| break :blk try t.clone(alloc);
                    break :blk try lhs.type.clone(alloc);
                },
            };

            const dst: TypedOperand = .{
                .operand = irBuilder.nextTemp(),
                .type = result_type,
            };
            try irBuilder.emit(.{ .lir = .{ .binop = .{
                .dst = dst,
                .op = op,
                .lhs = lhs,
                .rhs = rhs,
            } } }, alloc);
            return try dst.clone(alloc);
        },
        .UnaryOp => {
            const operand_obj = c.PyObject_GetAttrString(stmt, "operand");
            const src = try walkExpr(operand_obj, irBuilder, expected_type, alloc);
            const dst: TypedOperand = .{
                .operand = irBuilder.nextTemp(),
                .type = try src.type.clone(alloc),
            };
            const op = try getUnaryOp(stmt);
            try irBuilder.emit(.{ .lir = .{ .unaryop = .{
                .dst = dst,
                .op = op,
                .src = src,
            } } }, alloc);
            return try dst.clone(alloc);
        },
        .Constant => {
            const value_obj = c.PyObject_GetAttrString(stmt, "value");
            std.debug.assert(value_obj != null);
            const parsed_constant = try parseConstant(value_obj, expected_type, alloc);
            switch (parsed_constant) {
                .immediate => |imm| {
                    const constant_type = if (expected_type) |t|
                        try t.clone(alloc)
                    else
                        imm.toType();
                    const dst: TypedOperand = .{
                        .operand = irBuilder.nextTemp(),
                        .type = constant_type,
                    };
                    try irBuilder.emit(.{ .lir = .{ .move = .{
                        .dst = dst,
                        .src = .{ .constant = imm },
                    } } }, alloc);
                    return try dst.clone(alloc);
                },
                .composite => |comp| {
                    const dst: TypedOperand = .{
                        .operand = irBuilder.nextTemp(),
                        .type = comp.type,
                    };
                    try irBuilder.emit(.{ .list_literal = .{
                        .dst = dst,
                        .elements = comp.elements,
                    } }, alloc);
                    return try dst.clone(alloc);
                },
            }
        },
        // List(elts=[Constant(value=1), Constant(value=2), Constant(value=3)], ctx=Load())
        .List => {
            const elements = c.PyObject_GetAttrString(stmt, "elts");
            std.debug.assert(elements != null);
            const len = c.PyList_Size(elements);
            var result: ArrayList(ValueRef) = .empty;
            errdefer result.deinit(alloc);

            for (0..@intCast(len)) |i| {
                const elem = c.PyList_GetItem(elements, @as(isize, @intCast(i)));
                std.debug.assert(elem != null);
                const expected_elem_type: ?TypeInfo = if (expected_type) |t| try t.getElementType() else null;
                // [conditional] use constant instead of an operand if we can
                switch (getExprKind(elem)) {
                    .Constant => {
                        const value = c.PyObject_GetAttrString(elem, "value");

                        const constant = try parseConstant(value, expected_elem_type, alloc);
                        switch (constant) {
                            .immediate => |imm| {
                                try result.append(alloc, .{ .constant = imm });
                            },
                            .composite => |comp| {
                                const dst: TypedOperand = .{
                                    .operand = irBuilder.nextTemp(),
                                    .type = comp.type,
                                };
                                try irBuilder.emit(.{ .list_literal = .{
                                    .dst = dst,
                                    .elements = comp.elements,
                                } }, alloc);

                                try result.append(alloc, .{ .top = try dst.clone(alloc) });
                            },
                        }
                    },
                    else => {
                        const expr = try walkExpr(elem, irBuilder, expected_elem_type, alloc);
                        try result.append(alloc, .{ .top = expr });
                    },
                }
            }
            const dst_type: TypeInfo = if (expected_type) |t|
                try t.clone(alloc)
            else blk: {
                if (result.items.len == 0) return error.NoTypeFound;
                const elem_type = try result.items[0].toType(alloc);
                break :blk .{
                    .list = .{ .element = try elem_type.toOwnedPointer(alloc) },
                };
            };

            const dst: TypedOperand = .{
                .operand = irBuilder.nextTemp(),
                .type = dst_type,
            };
            try irBuilder.emit(.{ .list_literal = .{
                .dst = dst,
                .elements = try result.toOwnedSlice(alloc),
            } }, alloc);
            return try dst.clone(alloc);
        },
        // Tuple(elts=[Name(id='x', ctx=Load()), Name(id='y', ctx=Load())], ctx=Load())
        .Tuple => {
            const elts_obj = c.PyObject_GetAttrString(stmt, "elts");
            std.debug.assert(elts_obj != null);
            const len: usize = @intCast(c.PyList_Size(elts_obj));
            const expected_elements = if (expected_type) |t|
                switch (t) {
                    .tuple => |tup| blk: {
                        if (tup.elements.len != len) return error.MismatchingTypes;
                        break :blk tup.elements;
                    },
                    else => null,
                }
            else
                null;
            var elements = try alloc.alloc(ValueRef, len);
            var element_types = try alloc.alloc(TypeInfo, len);
            for (0..len) |i| {
                const elem_obj = c.PyList_GetItem(elts_obj, @intCast(i));
                std.debug.assert(elem_obj != null);
                const expected_elem_type: ?TypeInfo = if (expected_elements) |elem|
                    elem[i]
                else
                    null;
                const elem_op = try walkExpr(elem_obj, irBuilder, expected_elem_type, alloc);
                elements[i] = ValueRef{
                    .top = elem_op,
                };
                element_types[i] = try elem_op.type.clone(alloc);
            }

            const dst = irBuilder.nextTemp();
            const typed_dst: TypedOperand = .{
                .operand = dst,
                .type = .{ .tuple = .{ .elements = element_types } },
            };
            try irBuilder.emit(.{
                .tuple_literal = .{
                    .dst = typed_dst,
                    .elements = elements,
                },
            }, alloc);
            return try typed_dst.clone(alloc);
        },
        // Subscript(value=Name(id='items', ctx=Load()), slice=Constant(value=0), ctx=Load())
        .Subscript => {
            const value_obj = c.PyObject_GetAttrString(stmt, "value");
            std.debug.assert(value_obj != null);

            const slice = c.PyObject_GetAttrString(stmt, "slice");

            const value = try walkExpr(value_obj, irBuilder, null, alloc);
            const index = try walkExpr(slice, irBuilder, null, alloc);

            switch (value.type) {
                .list => |list| {
                    if (index.type != .i64 and index.type != .i32 and index.type != .any) {
                        return error.ArrayIndexMustBeInt;
                    }
                    const elem_type = list.element.*;

                    const dst: TypedOperand = .{
                        .operand = irBuilder.nextTemp(),
                        .type = try elem_type.clone(alloc),
                    };
                    try irBuilder.emit(.{ .subscript = .{
                        .dst = dst,
                        .src = value,
                        .index = index,
                    } }, alloc);
                    return try dst.clone(alloc);
                },
                .tuple => |tuple| {
                    if (getExprKind(slice) != .Constant) {
                        return error.TupleIndexMustBeConstant;
                    }
                    const index_value_obj = c.PyObject_GetAttrString(slice, "value");
                    std.debug.assert(index_value_obj != null);

                    const raw_index = c.PyLong_AsLong(index_value_obj);
                    const tuple_index: usize = @intCast(raw_index);
                    if (tuple_index < 0) return error.TupleIndexOutOfBounds;
                    if (tuple_index >= tuple.elements.len) return error.TupleIndexOutOfBounds;

                    const dst: TypedOperand = .{
                        .operand = irBuilder.nextTemp(),
                        .type = try tuple.elements[tuple_index].clone(alloc),
                    };
                    try irBuilder.emit(.{ .subscript = .{
                        .dst = dst,
                        .src = value,
                        .index = index,
                    } }, alloc);
                    return try dst.clone(alloc);
                },
                .instance => |instance| {
                    const class = irBuilder.getClass(instance.class_id);
                    const getitem_method = class.findMethod("__getitem__") orelse {
                        return error.CantFindGetMethod;
                    };
                    const getitem_function = irBuilder.getFunction(getitem_method.function_id) orelse {
                        return error.CantFindGetMethod;
                    };

                    var bindings: TypeBindings = .init(alloc);
                    defer bindings.deinit(alloc);
                    const return_type = try bindings.inferReturnType(getitem_function, &.{ value, index }, alloc);

                    const dst: TypedOperand = .{
                        .operand = irBuilder.nextTemp(),
                        .type = return_type,
                    };

                    try irBuilder.emit(.{ .subscript = .{
                        .dst = dst,
                        .src = value,
                        .index = index,
                    } }, alloc);

                    return try dst.clone(alloc);
                },
                else => return error.UnsupportedIndex,
            }
        },
        .Name => {
            const id_obj = c.PyObject_GetAttrString(stmt, "id");
            std.debug.assert(id_obj != null);

            const id = c.PyUnicode_AsUTF8(id_obj);
            std.debug.assert(id != null);

            const name = std.mem.span(id);
            const localId = try irBuilder.getOrCreateLocal(name, null, alloc);

            if (irBuilder.local_values.get(localId)) |value| {
                return try value.clone(alloc);
            }

            if (irBuilder.findImportModule(name)) |module_id| {
                return .{
                    .operand = .unknown,
                    .type = .{ .module = module_id },
                };
            }

            if (irBuilder.findFunction(name)) |function| {
                var params = try alloc.alloc(TypeInfo, function.params.len);
                for (function.params, 0..) |param, i| {
                    params[i] = try param.type.clone(alloc);
                }
                const function_dst: TypedOperand = .{
                    .operand = function.nextTemp(),
                    .type = .{ .callable = .{
                        .params = params,
                        .returns = try (try function.return_type.clone(alloc)).toOwnedPointer(alloc),
                    } },
                };
                // declare function we will return
                try irBuilder.emit(.{
                    .function_ref = .{
                        .dst = function_dst,
                        .function_name = try alloc.dupe(u8, function.name),
                    },
                }, alloc);
                return try function_dst.clone(alloc);
            }
            const local = try irBuilder.locals.items[localId].clone(alloc);
            const dst: TypedOperand = .{
                .operand = irBuilder.nextTemp(),
                .type = local.type,
            };
            try irBuilder.emit(.{ .lir = .{ .load_local = .{
                .dst = dst,
                .local = local,
            } } }, alloc);
            return try dst.clone(alloc);
        },
        // Compare(left=Constant(1),ops=[Lt()],comparators=[Constant(2)])
        .Compare => {
            const left_obj = c.PyObject_GetAttrString(stmt, "left");
            const comparators = c.PyObject_GetAttrString(stmt, "comparators");
            std.debug.assert(left_obj != null);
            std.debug.assert(comparators != null);
            const right_obj = c.PyList_GetItem(comparators, 0);
            std.debug.assert(right_obj != null);

            const lhs = try walkExpr(left_obj, irBuilder, null, alloc);
            const rhs = try walkExpr(right_obj, irBuilder, null, alloc);
            const dst: TypedOperand = .{ .operand = irBuilder.nextTemp(), .type = .bool };
            const op = try getCompareOp(stmt);

            try irBuilder.emit(.{ .lir = .{ .compare = .{
                .dst = dst,
                .lhs = lhs,
                .op = op,
                .rhs = rhs,
            } } }, alloc);

            return try dst.clone(alloc);
        },
        .Call => {
            const func = c.PyObject_GetAttrString(stmt, "func");
            std.debug.assert(func != null);
            const func_kind = getPyType(func);

            if (std.mem.eql(u8, func_kind, "Name")) {
                return walkNamedCall(stmt, func, irBuilder, expected_type, alloc);
            } else if (std.mem.eql(u8, func_kind, "Attribute")) {
                return walkMethodCall(stmt, func, irBuilder, alloc);
            } else if (std.mem.eql(u8, func_kind, "Subscript")) {
                return walkGenericCall(stmt, func, irBuilder, alloc);
            }
            std.debug.print("unsupported callee type: {s}\n", .{func_kind});
            return error.UnsupportedCallee;
        },
        // IfExp(test=Name(id='c', ctx=Load()), body=Constant(value='FALSE'), orelse=Constant(value='TRUE'))
        .IfExp => {
            const test_obj = c.PyObject_GetAttrString(stmt, "test");
            const body_obj = c.PyObject_GetAttrString(stmt, "body");
            const orelse_obj = c.PyObject_GetAttrString(stmt, "orelse");
            std.debug.assert(test_obj != null);
            std.debug.assert(body_obj != null);
            std.debug.assert(orelse_obj != null);

            const condition = try walkExpr(test_obj, irBuilder, null, alloc);
            const if_value = try walkExpr(body_obj, irBuilder, null, alloc);
            const else_value = try walkExpr(orelse_obj, irBuilder, null, alloc);

            const dst: TypedOperand = .{
                .operand = irBuilder.nextTemp(),
                .type = try if_value.type.clone(alloc),
            };
            try irBuilder.emit(.{ .lir = .{ .select = .{
                .dst = dst,
                .condition = condition,
                .if_value = .{ .top = if_value },
                .else_value = .{ .top = else_value },
            } } }, alloc);

            return try dst.clone(alloc);
        },
        // Attribute(value=Name(id='self', ctx=Load()), attr='name', ctx=Store())
        .Attribute => {
            const value = c.PyObject_GetAttrString(stmt, "value");
            std.debug.assert(value != null);
            const instance_expr = try walkExpr(value, irBuilder, null, alloc);
            const instance = switch (instance_expr.type) {
                .instance => |id| id,
                else => return error.UnexpectedType,
            };

            const name_obj = c.PyObject_GetAttrString(stmt, "attr");
            std.debug.assert(name_obj != null);
            const raw_name = c.PyUnicode_AsUTF8(name_obj);
            std.debug.assert(raw_name != null);

            const class = irBuilder.getClass(instance.class_id);
            const name = std.mem.span(raw_name);
            const field = class.findField(name) orelse {
                std.debug.print("cant find {s}\n", .{name});
                return error.CantFindField;
            };

            const dst: TypedOperand = .{
                .operand = irBuilder.nextTemp(),
                .type = try field.type.clone(alloc),
            };
            try irBuilder.emit(.{ .field_load = .{
                .dst = dst,
                .instance = instance_expr,
                .offset = field.offset,
            } }, alloc);
            return try dst.clone(alloc);
        },
        .Unknown => {
            const name = getPyType(stmt);
            std.debug.print("unsupported expr type: {s}: ", .{name});
            printAstDump(stmt);
            return error.ExprUnknown;
        },
    }
}

// Expr(value=Call(func=Name(id="print"),args=[BinOp(...)]))
fn walkNamedCall(
    stmt: *PyObject,
    func: *PyObject,
    irBuilder: *IrBuilder,
    expected_type: ?TypeInfo,
    alloc: std.mem.Allocator,
) anyerror!TypedOperand {
    const func_id = c.PyObject_GetAttrString(func, "id");
    std.debug.assert(func_id != null);

    const name = c.PyUnicode_AsUTF8(func_id);
    std.debug.assert(name != null);

    const args = c.PyObject_GetAttrString(stmt, "args");
    std.debug.assert(args != null);

    const builtin = getBuiltinCall(std.mem.span(name));

    if (builtin) |b| {
        switch (b) {
            .Print => {
                std.debug.assert(c.PyList_Size(args) == 1);
                const arg0 = c.PyList_GetItem(args, 0);
                std.debug.assert(arg0 != null);
                const src = try walkExpr(arg0, irBuilder, null, alloc);

                const keywords = c.PyObject_GetAttrString(stmt, "keywords");
                std.debug.assert(keywords != null);
                var end: ?TypedOperand = null;
                const keyword_length = c.PyList_Size(keywords);
                std.debug.assert(keyword_length == 0 or keyword_length == 1);
                if (keyword_length == 1) {
                    const end_obj = c.PyList_GetItem(keywords, 0);
                    std.debug.assert(end_obj != null);
                    const arg_obj = c.PyObject_GetAttrString(end_obj, "arg");
                    std.debug.assert(arg_obj != null);
                    const label = c.PyUnicode_AsUTF8(arg_obj);
                    if (!std.mem.eql(u8, std.mem.span(label), "end")) {
                        return error.ExpectedEnd;
                    }
                    const end_value_obj = c.PyObject_GetAttrString(end_obj, "value");
                    std.debug.assert(end_value_obj != null);
                    end = try walkExpr(end_value_obj, irBuilder, null, alloc);
                }

                try irBuilder.emit(Instruction{ .print = .{
                    .src = src,
                    .end = end,
                } }, alloc);
                return .{ .operand = .unknown, .type = .any };
            },
            .Write => {
                std.debug.assert(c.PyList_Size(args) == 3);
                const arg0 = c.PyList_GetItem(args, 0);
                std.debug.assert(arg0 != null);
                const fd = try walkExpr(arg0, irBuilder, null, alloc);
                const arg1 = c.PyList_GetItem(args, 1);
                std.debug.assert(arg1 != null);
                const buf = try walkExpr(arg1, irBuilder, null, alloc);
                const arg2 = c.PyList_GetItem(args, 2);
                std.debug.assert(arg2 != null);
                const len = try walkExpr(arg2, irBuilder, null, alloc);
                switch (buf.type) {
                    .list => {
                        // gross but we need to increment past the book keeping size value
                        const eight: TypedOperand = .{ .operand = irBuilder.nextTemp(), .type = .i64 };
                        try irBuilder.emit(.{ .lir = .{ .move = .{
                            .dst = eight,
                            .src = .{ .constant = .{ .i64 = 8 } },
                        } } }, alloc);
                        const data: TypedOperand = .{
                            .operand = irBuilder.nextTemp(),
                            .type = .ptr,
                        };
                        // write returns a pointer
                        try irBuilder.emit(.{ .lir = .{ .binop = .{
                            .dst = data,
                            .lhs = buf,
                            .op = .add,
                            .rhs = eight,
                        } } }, alloc);
                        const write_args = try alloc.alloc(TypedOperand, 3);
                        write_args[0] = fd;
                        write_args[1] = try data.clone(alloc);
                        write_args[2] = len;
                        try irBuilder.emit(.{
                            .function_call = .{
                                .dst = null,
                                .args = write_args,
                                .callee = .{ .direct = try alloc.dupe(u8, "write") },
                            },
                        }, alloc);
                    },
                    .tuple => {
                        const write_args = try alloc.alloc(TypedOperand, 3);
                        write_args[0] = fd;
                        write_args[1] = buf;
                        write_args[2] = len;
                        try irBuilder.emit(.{
                            .function_call = .{
                                .dst = null,
                                .args = write_args,
                                .callee = .{ .direct = try alloc.dupe(u8, "write") },
                            },
                        }, alloc);
                    },
                    else => |e| {
                        std.debug.print("cant write type {s}\n", .{@tagName(e)});
                        return error.UnsupportedWriteType;
                    },
                }
                return .{ .operand = .unknown, .type = .void };
            },
            .Len => {
                std.debug.assert(c.PyList_Size(args) == 1);
                const arg0 = c.PyList_GetItem(args, 0);
                std.debug.assert(arg0 != null);
                const value = try walkExpr(arg0, irBuilder, null, alloc);
                const dst: TypedOperand = .{
                    .operand = irBuilder.nextTemp(),
                    // HACK: dont hardcode width
                    .type = .i32,
                };
                try irBuilder.emit(.{ .len = .{
                    .dst = dst,
                    .value = value,
                } }, alloc);
                return try dst.clone(alloc);
            },
            // Call(func=Name(id='range', ctx=Load()), args=[Constant(value=0), Constant(value=10)])
            .Range => {
                const bounds: RangeBounds = switch (c.PyList_Size(args)) {
                    1 => blk: {
                        const endItem = c.PyList_GetItem(args, 0);
                        std.debug.assert(endItem != null);
                        const end = try walkExpr(endItem, irBuilder, null, alloc);

                        const start: TypedOperand = .{
                            .operand = irBuilder.nextTemp(),
                            .type = try end.type.clone(alloc),
                        };
                        const zero: ConstValue = switch (start.type) {
                            .i64 => .{ .i64 = 0 },
                            .i32 => .{ .i32 = 0 },
                            else => return error.InvalidRangeType,
                        };
                        try irBuilder.emit(.{ .lir = .{ .move = .{
                            .dst = start,
                            .src = .{ .constant = zero },
                        } } }, alloc);

                        break :blk .{
                            .start = try start.clone(alloc),
                            .end = end,
                        };
                    },
                    2 => blk: {
                        const startItem = c.PyList_GetItem(args, 0);
                        std.debug.assert(startItem != null);
                        const start = try walkExpr(startItem, irBuilder, null, alloc);
                        const endItem = c.PyList_GetItem(args, 1);
                        std.debug.assert(endItem != null);
                        const end = try walkExpr(endItem, irBuilder, null, alloc);
                        break :blk RangeBounds{ .start = start, .end = end };
                    },
                    else => return error.InvalidBounds,
                };

                const dst = irBuilder.nextTemp();

                const type_: TypeInfo = .{
                    .lazy = .{
                        .value = try TypeInfo.toOwnedPointer(.{
                            .iterable = .{
                                // .element = try TypeInfo.toOwnedPointer(.i64, alloc),
                                .element = try (try bounds.end.type.clone(alloc)).toOwnedPointer(alloc),
                            },
                        }, alloc),
                    },
                };
                const typed_dst = TypedOperand{ .operand = dst, .type = type_ };
                try irBuilder.emit(.{ .range = .{
                    .dst = typed_dst,
                    .start = bounds.start,
                    .end = bounds.end,
                } }, alloc);
                return try typed_dst.clone(alloc);
            },
            .Int, .I32, .Float => |t| {
                std.debug.assert(c.PyList_Size(args) == 1);
                const arg0 = c.PyList_GetItem(args, 0);
                std.debug.assert(arg0 != null);
                const dst_type: TypeInfo = switch (t) {
                    .Int => .i64,
                    .I32 => .i32,
                    .Float => .f64,
                    else => unreachable,
                };
                const value = try walkExpr(arg0, irBuilder, null, alloc);
                const dst: TypedOperand = .{
                    .operand = irBuilder.nextTemp(),
                    .type = dst_type,
                };
                try irBuilder.emit(.{ .lir = .{ .cast = .{
                    .dst = dst,
                    .dst_target_type = dst_type,
                    .src = value,
                } } }, alloc);
                return try dst.clone(alloc);
            },
            .GlobalIdx => {
                std.debug.assert(c.PyList_Size(args) == 1);
                const arg_obj = c.PyList_GetItem(args, 0);
                const arg = try walkExpr(arg_obj, irBuilder, null, alloc);

                const dst: TypedOperand = .{
                    .operand = irBuilder.nextTemp(),
                    .type = .i32,
                };

                try irBuilder.emit(.{ .global_idx = .{
                    .dst = dst,
                    .axis = .{ .top = arg },
                } }, alloc);

                return try dst.clone(alloc);
            },
            .Max => {
                std.debug.assert(c.PyList_Size(args) == 2);
                const lhs_obj = c.PyList_GetItem(args, 0);
                const lhs = try walkExpr(lhs_obj, irBuilder, expected_type, alloc);
                const rhs_obj = c.PyList_GetItem(args, 1);
                const rhs = try walkExpr(rhs_obj, irBuilder, lhs.type, alloc);

                std.debug.assert(lhs.type.equal(rhs.type));

                const compare: TypedOperand = .{
                    .operand = irBuilder.nextTemp(),
                    .type = .bool,
                };

                try irBuilder.emit(.{ .lir = .{ .compare = .{
                    .dst = compare,
                    .lhs = lhs,
                    .op = .gt,
                    .rhs = rhs,
                } } }, alloc);

                const dst: TypedOperand = .{
                    .operand = irBuilder.nextTemp(),
                    .type = try lhs.type.clone(alloc),
                };

                try irBuilder.emit(.{ .lir = .{ .select = .{
                    .dst = dst,
                    .condition = compare,
                    .if_value = .{ .top = try lhs.clone(alloc) },
                    .else_value = .{ .top = try rhs.clone(alloc) },
                } } }, alloc);

                return try dst.clone(alloc);
            },
            .Exp => {
                std.debug.assert(c.PyList_Size(args) == 1);
                const arg = c.PyList_GetItem(args, 0);
                std.debug.assert(arg != null);
                const callee_args = try alloc.alloc(TypedOperand, 1);
                callee_args[0] = try walkExpr(arg, irBuilder, null, alloc);
                const dst: TypedOperand = .{
                    .operand = irBuilder.nextTemp(),
                    .type = .f64,
                };
                // this pattern only works on the cpu
                try irBuilder.emit(.{ .function_call = .{
                    .dst = dst,
                    .callee = .{ .direct = try alloc.dupe(u8, "exp") },
                    .args = callee_args,
                } }, alloc);
                return try dst.clone(alloc);
            },
            .Exp2 => {
                std.debug.assert(c.PyList_Size(args) == 1);
                const arg = c.PyList_GetItem(args, 0);
                std.debug.assert(arg != null);
                const callee_arg = try walkExpr(arg, irBuilder, null, alloc);
                const dst: TypedOperand = .{
                    .operand = irBuilder.nextTemp(),
                    .type = try callee_arg.type.clone(alloc),
                };
                try irBuilder.emit(.{ .lir = .{ .unaryop = .{
                    .dst = dst,
                    .op = .exp2,
                    .src = callee_arg,
                } } }, alloc);
                return try dst.clone(alloc);
            },
            .Type => {
                std.debug.assert(c.PyList_Size(args) == 1);
                const arg = c.PyList_GetItem(args, 0);
                std.debug.assert(arg != null);
                const value = try walkExpr(arg, irBuilder, null, alloc);
                defer value.deinit(alloc);

                const type_name = try value.type.toString(alloc);
                defer alloc.free(type_name);

                const string = try makeStringLiteral(type_name, alloc);
                const composite = string.composite;
                const dst: TypedOperand = .{
                    .operand = irBuilder.nextTemp(),
                    .type = composite.type,
                };
                try irBuilder.emit(.{ .list_literal = .{
                    .dst = dst,
                    .elements = composite.elements,
                } }, alloc);
                return try dst.clone(alloc);
            },
        }
    }
    // class constructor
    const constructor_init = if (irBuilder.findClass(std.mem.span(name))) |class| blk: {
        const init_method = class.findMethod("__init__") orelse {
            return error.CantFindInit;
        };
        break :blk irBuilder.getFunction(init_method.function_id) orelse {
            return error.CantFindInit;
        };
    } else null;
    if (constructor_init) |init| {
        if (init.params.len != c.PyList_Size(args) + 1) {
            return error.InvalidArgCount;
        }
    }

    // arguments are params only declared at call site
    var arguments: ArrayList(TypedOperand) = .empty;
    errdefer {
        for (arguments.items) |arg| {
            arg.deinit(alloc);
        }
        arguments.deinit(alloc);
    }
    for (0..@intCast(c.PyList_Size(args))) |i| {
        const arg_obj = c.PyList_GetItem(args, @intCast(i));
        std.debug.assert(arg_obj != null);
        const param_type = if (constructor_init) |init|
            init.params[i + 1].type
        else
            null;
        // dont infer type from generic args
        const expected_arg_type = if (param_type) |t|
            if (!t.containsGenericVariable()) t else null
        else
            null;
        const arg = try walkExpr(arg_obj, irBuilder, expected_arg_type, alloc);
        try arguments.append(alloc, arg);
    }
    const name_slice = std.mem.span(name);

    if (irBuilder.getLocal(name_slice) catch null) |local_id| {
        if (irBuilder.local_values.get(local_id)) |callee| {
            if (callee.type == .callable) {
                const maybe_dst: ?TypedOperand = if (callee.type.callable.returns.* == .void)
                    null
                else
                    .{
                        .operand = irBuilder.nextTemp(),
                        .type = callee.type.callable.returns.*,
                    };

                try irBuilder.emit(.{
                    .function_call = .{
                        .callee = .{ .indirect = try callee.clone(alloc) },
                        .dst = if (maybe_dst) |dst| try dst.clone(alloc) else null,
                        .args = try arguments.toOwnedSlice(alloc),
                    },
                }, alloc);

                if (maybe_dst) |dst| return dst;
                return TypedOperand{ .operand = .unknown, .type = .void };
            }
        }
    }

    if (irBuilder.findFunction(std.mem.span(name))) |function| {
        return emitResolvedCall(function, name_slice, &arguments, irBuilder, alloc);
    }

    // class constructor
    if (irBuilder.findClass(std.mem.span(name))) |class| {
        const init = constructor_init orelse return error.CantFindInit;
        var bindings: TypeBindings = .init(alloc);
        defer bindings.deinit(alloc);

        for (init.params[1..], arguments.items) |param, arg| {
            try TypeInfo.unify(param.type, arg.type, &bindings, alloc);
        }

        const instance_args = try alloc.alloc(TypeInfo, class.type_params.len);
        errdefer alloc.free(instance_args);

        for (class.type_params, 0..) |type_param, i| {
            const bound_type = bindings.get(type_param.id) orelse {
                return error.ExpectedBinding;
            };
            instance_args[i] = try bound_type.clone(alloc);
        }

        const dst: TypedOperand = .{
            .operand = irBuilder.nextTemp(),
            .type = .{
                .instance = .{
                    .class_id = class.id,
                    .args = instance_args,
                },
            },
        };
        try irBuilder.emit(.{ .class_init = .{
            .dst = dst,
            .class_id = class.id,
            .args = try arguments.toOwnedSlice(alloc),
        } }, alloc);
        return try dst.clone(alloc);
    }

    std.debug.print("cant find function {s}\n", .{name});
    return error.CantFindFunction;
}

// Call(func=Attribute(value=Name(id='audi', ctx=Load()), attr='print_speed', ctx=Load()))
fn walkMethodCall(stmt: *PyObject, func: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) anyerror!TypedOperand {
    const instance_obj = c.PyObject_GetAttrString(func, "value");
    std.debug.assert(instance_obj != null);

    const method_obj = c.PyObject_GetAttrString(func, "attr");
    std.debug.assert(method_obj != null);
    const method_name_raw = c.PyUnicode_AsUTF8(method_obj);
    std.debug.assert(method_name_raw != null);
    const method_name = std.mem.span(method_name_raw);

    var self: ?TypedOperand = null;
    const method = blk: {
        if (std.mem.eql(u8, getPyType(instance_obj), "Name")) {
            const id_obj = PyObject.GetAttrString(instance_obj, "id");
            std.debug.assert(id_obj != null);
            const raw_name = c.PyUnicode_AsUTF8(id_obj);
            std.debug.assert(raw_name != null);
            if (irBuilder.findClass(std.mem.span(raw_name))) |class| {
                const method_info = class.findMethod(method_name) orelse {
                    return error.CantFindMethod;
                };
                if (!method_info.is_static) return error.ExpectedInstance;
                const method = irBuilder.getFunction(method_info.function_id) orelse {
                    return error.CantFindFunction;
                };
                break :blk method;
            }
        }
        const receiver_expr = try walkExpr(instance_obj, irBuilder, null, alloc);
        const method = switch (receiver_expr.type) {
            .instance => |inst| module_blk: {
                self = receiver_expr;
                const class = irBuilder.getClass(inst.class_id);
                const method_info = class.findMethod(method_name) orelse {
                    return error.CantFindMethod;
                };
                break :module_blk irBuilder.getFunction(method_info.function_id) orelse {
                    return error.CantFindFunction;
                };
            },
            .module => |module_id| {
                break :blk irBuilder.getModuleFunction(module_id, method_name) orelse {
                    return error.CantFindFunction;
                };
            },
            else => return error.ExpectedInstance,
        };
        break :blk method;
    };

    var arguments: ArrayList(TypedOperand) = .empty;
    errdefer {
        for (arguments.items) |arg| {
            arg.type.deinit(alloc);
        }
        arguments.deinit(alloc);
    }

    // instance expression, <remaining args>
    if (self) |value| {
        try arguments.append(alloc, value);
    }
    const args_obj = c.PyObject_GetAttrString(stmt, "args");
    std.debug.assert(args_obj != null);
    for (0..@intCast(c.PyList_Size(args_obj))) |i| {
        const arg_obj = c.PyList_GetItem(args_obj, @intCast(i));
        std.debug.assert(arg_obj != null);
        const arg = try walkExpr(arg_obj, irBuilder, null, alloc);
        try arguments.append(alloc, arg);
    }

    return emitResolvedCall(method, method.name, &arguments, irBuilder, alloc);
}

// Subscript(value=Name(id='MiniTorch', ctx=Load()), slice=Name(id='int', ctx=Load()), ctx=Load())
fn walkGenericCall(stmt: *PyObject, func: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) anyerror!TypedOperand {
    const value = PyObject.GetAttrString(func, "value");
    std.debug.assert(value != null);

    if (!std.mem.eql(u8, getPyType(value), "Name")) {
        return error.UnsupportedGenericCall;
    }
    const name_obj = PyObject.GetAttrString(value, "id");
    std.debug.assert(name_obj != null);
    const raw_name = PyObject.PyUnicode_AsUTF8(name_obj);
    std.debug.assert(raw_name != null);
    const name = std.mem.span(raw_name);

    const class = irBuilder.findClass(name) orelse {
        return error.CantFindClass;
    };

    const slice_obj = PyObject.GetAttrString(func, "slice");
    std.debug.assert(slice_obj != null);

    var type_args: ArrayList(TypeInfo) = .empty;
    errdefer {
        for (type_args.items) |arg| {
            arg.deinit(alloc);
        }
        type_args.deinit(alloc);
    }

    try type_args.append(
        alloc,
        try parseTypeAnnotation(slice_obj, irBuilder, alloc),
    );

    if (type_args.items.len != class.type_params.len) {
        return error.InvalidTypeArgumentCount;
    }

    const args_list = c.PyObject_GetAttrString(stmt, "args");
    std.debug.assert(args_list != null);
    var arguments: ArrayList(TypedOperand) = .empty;
    errdefer {
        for (arguments.items) |arg| {
            arg.deinit(alloc);
        }
        arguments.deinit(alloc);
    }
    for (0..@intCast(c.PyList_Size(args_list))) |i| {
        const arg_obj = c.PyList_GetItem(args_list, @intCast(i));
        std.debug.assert(arg_obj != null);
        const arg = try walkExpr(arg_obj, irBuilder, null, alloc);
        try arguments.append(alloc, arg);
    }

    const dst: TypedOperand = .{
        .operand = irBuilder.nextTemp(),
        .type = .{ .instance = .{
            .class_id = class.id,
            .args = try type_args.toOwnedSlice(alloc),
        } },
    };

    try irBuilder.emit(.{ .class_init = .{
        .dst = dst,
        .args = try arguments.toOwnedSlice(alloc),
        .class_id = class.id,
    } }, alloc);

    return dst;
}

// If(test=Compare(...), body=[...], orelse=[...])
pub fn walkIf(stmt: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) anyerror!void {
    var before_values = try irBuilder.cloneLocalValues(alloc);
    defer IrBuilder.deinitLocalValues(&before_values, alloc);

    const test_ = c.PyObject_GetAttrString(stmt, "test");
    const body = c.PyObject_GetAttrString(stmt, "body");
    const orelse_ = c.PyObject_GetAttrString(stmt, "orelse");

    const then_block = try irBuilder.newBlock(alloc);
    const else_block = try irBuilder.newBlock(alloc);
    const merge_block = try irBuilder.newBlock(alloc);

    const condition = try walkExpr(test_, irBuilder, null, alloc);
    try irBuilder.emit(.{ .lir = .{ .branch = .{
        .condition = condition,
        .then_block = then_block,
        .else_block = else_block,
    } } }, alloc);
    try irBuilder.addSuccessor(irBuilder.current_block, then_block, alloc);
    try irBuilder.addSuccessor(irBuilder.current_block, else_block, alloc);

    // then block
    irBuilder.setCurrentBlock(then_block);
    // restore in case condition set variables
    try irBuilder.restoreLocalValues(&before_values, alloc);
    try walkStmtList(body, irBuilder, alloc);
    const then_exit_block = irBuilder.current_block;
    // save then locals
    var then_values = try irBuilder.cloneLocalValues(alloc);
    defer IrBuilder.deinitLocalValues(&then_values, alloc);
    try irBuilder.emit(.{ .lir = .{
        .jump = .{ .target = merge_block },
    } }, alloc);
    try irBuilder.addSuccessor(then_exit_block, merge_block, alloc);

    // else block
    irBuilder.setCurrentBlock(else_block);
    // restore in case condition set variables
    try irBuilder.restoreLocalValues(&before_values, alloc);
    try walkStmtList(orelse_, irBuilder, alloc);
    // save else locals
    const else_exit_block = irBuilder.current_block;
    var else_values = try irBuilder.cloneLocalValues(alloc);
    defer IrBuilder.deinitLocalValues(&else_values, alloc);
    try irBuilder.emit(.{ .lir = .{
        .jump = .{ .target = merge_block },
    } }, alloc);
    try irBuilder.addSuccessor(else_exit_block, merge_block, alloc);

    irBuilder.setCurrentBlock(merge_block);
    // get locals orelse use branch value
    irBuilder.clearLocalValues(alloc);
    var all_locals = std.AutoHashMap(LocalId, void).init(alloc);
    defer all_locals.deinit();

    var before_it = before_values.keyIterator();
    while (before_it.next()) |val| {
        try all_locals.put(val.*, {});
    }

    var then_it = then_values.keyIterator();
    while (then_it.next()) |val| {
        try all_locals.put(val.*, {});
    }

    var else_it = else_values.keyIterator();
    while (else_it.next()) |val| {
        try all_locals.put(val.*, {});
    }

    var it = all_locals.keyIterator();
    while (it.next()) |local| {
        const before_value = before_values.get(local.*);
        const then_value = then_values.get(local.*) orelse before_value;
        const else_value = else_values.get(local.*) orelse before_value;

        const has_before = before_value != null;
        const has_then = then_value != null;
        const has_else = else_value != null;
        // variable isn't touch so no need to use a phi
        if (has_then and has_else and then_value.?.equal(else_value.?)) {
            try irBuilder.local_values.put(local.*, try before_value.?.clone(alloc));
        }
        // emit a phi
        else if (has_then and has_else) {
            const dst = irBuilder.nextTemp();
            const inputs = try alloc.dupe(PhiInput, &.{
                .{ .pred = then_exit_block, .value = try then_value.?.clone(alloc) },
                .{ .pred = else_exit_block, .value = try else_value.?.clone(alloc) },
            });

            const typed_dst: TypedOperand = .{
                .operand = dst,
                .type = try then_value.?.type.clone(alloc),
            };
            try irBuilder.emit(.{
                .phi = .{ .dst = typed_dst, .inputs = inputs },
            }, alloc);
            try irBuilder.local_values.put(local.*, try typed_dst.clone(alloc));
        } else if (!has_before and ((has_then and !has_else) or (!has_then and has_else))) {
            continue;
        } else {
            return error.NotImplemented;
        }
    }
}

//              ------------
//              |          |
//              v          |
// entry -> condition -> body
//              |
//              v
//             exit
// While(test=Compare(...), body=[...], orelse=[...])
pub fn walkWhile(stmt: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) anyerror!void {
    const test_ = c.PyObject_GetAttrString(stmt, "test");
    const body = c.PyObject_GetAttrString(stmt, "body");
    const orelse_ = c.PyObject_GetAttrString(stmt, "orelse");
    std.debug.assert(test_ != null);
    std.debug.assert(body != null);
    std.debug.assert(orelse_ != null);

    const callback = struct {
        fn loop(input_body: LoopBody, carries: []LoopCarry, irBuilder_: *IrBuilder, alloc_: std.mem.Allocator) anyerror!void {
            _ = carries;
            const body_ = switch (input_body) {
                .stmt_list => |sl| sl,
                else => return error.BadState,
            };
            try walkStmtList(body_, irBuilder_, alloc_);
        }
    };

    try walkLoop(irBuilder, .{ .expr = test_ }, LoopBody{ .stmt_list = body }, &.{}, callback.loop, orelse_, alloc);
}

// arr = [...] # range
// len = len(arr)
// index0 = 0
//
// condition:
//   index = phi(entry: index0, body: index_next)
//   keep_going = (index < len)
//   branch keep_going body exit
//
// body:
//   value = arr[index]
//   body
//   index_next = index + 1
//   jump condition
//
// exit:
pub fn walkFor(stmt: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) anyerror!void {
    const target = c.PyObject_GetAttrString(stmt, "target");
    std.debug.assert(target != null);
    const target_name_obj = c.PyObject_GetAttrString(target, "id");
    std.debug.assert(target_name_obj != null);
    const target_name_raw = c.PyUnicode_AsUTF8(target_name_obj);
    std.debug.assert(target_name_raw != null);
    const target_name = std.mem.span(target_name_raw);

    const iter = c.PyObject_GetAttrString(stmt, "iter");
    std.debug.assert(iter != null);

    const expr = try walkExpr(iter, irBuilder, null, alloc);
    std.debug.assert(expr.type.isIterable());

    const index0: TypedOperand = .{
        .operand = irBuilder.nextTemp(),
        .type = try expr.type.getElementType(),
    };
    const zero: ConstValue = switch (index0.type) {
        .i64 => .{ .i64 = 0 },
        .i32 => .{ .i32 = 0 },
        else => return error.InvalidRange,
    };
    try irBuilder.emit(.{ .lir = .{ .move = .{
        .dst = index0,
        .src = .{ .constant = zero },
    } } }, alloc);

    const callback = struct {
        fn loop(input_body_: LoopBody, carries: []LoopCarry, irBuilder_: *IrBuilder, alloc_: std.mem.Allocator) anyerror!void {
            const body_ = switch (input_body_) {
                .for_loop => |fl| fl,
                else => return error.BadState,
            };
            // value = arr[index]
            const index = carries[0].current;
            const value = irBuilder_.nextTemp();
            const iterable = if (body_.iterator_local) |local|
                irBuilder_.local_values.get(local) orelse return error.NotFound
            else
                body_.iterator;
            switch (iterable.type) {
                .tuple => {
                    try irBuilder_.emit(.{ .subscript = .{
                        .dst = .{ .operand = value, .type = .any },
                        .src = try iterable.clone(alloc_),
                        .index = try index.clone(alloc_),
                    } }, alloc_);
                },
                .list => {
                    try irBuilder_.emit(.{ .subscript = .{
                        .dst = .{ .operand = value, .type = .any },
                        .src = try iterable.clone(alloc_),
                        .index = try index.clone(alloc_),
                    } }, alloc_);
                },
                .iterable => {
                    try irBuilder_.emit(.{ .subscript = .{
                        .dst = .{ .operand = value, .type = .any },
                        .src = try iterable.clone(alloc_),
                        .index = try index.clone(alloc_),
                    } }, alloc_);
                },
                .lazy => {
                    try irBuilder_.emit(.{ .subscript = .{
                        .dst = .{ .operand = value, .type = .ptr },
                        .src = try iterable.clone(alloc_),
                        .index = try index.clone(alloc_),
                    } }, alloc_);
                },
                else => return error.CantIndexInto,
            }

            const elem_type = try iterable.type.getElementType();
            const local = try irBuilder_.getOrCreateLocal(
                body_.condition_var_name,
                try elem_type.clone(alloc_),
                alloc_,
            );
            const typed_value: TypedOperand = .{
                .operand = value,
                .type = elem_type,
            };
            try irBuilder_.local_values.put(local, typed_value);
            try irBuilder_.emit(.{ .lir = .{ .store_local = .{
                .local = .{
                    .id = local,
                    .name = try alloc_.dupe(u8, body_.condition_var_name),
                    .type = try elem_type.clone(alloc_),
                },
                .src = typed_value,
            } } }, alloc_);

            try walkStmtList(body_.stmt_list, irBuilder_, alloc_);
            // index += 1
            const one: TypedOperand = .{
                .operand = irBuilder_.nextTemp(),
                .type = try index.type.clone(alloc_),
            };
            const one_value: ConstValue = switch (one.type) {
                .i64 => .{ .i64 = 1 },
                .i32 => .{ .i32 = 1 },
                else => return error.InvalidRange,
            };
            try irBuilder_.emit(.{ .lir = .{ .move = .{
                .dst = one,
                .src = .{ .constant = one_value },
            } } }, alloc_);

            const index_next: TypedOperand = .{
                .operand = irBuilder_.nextTemp(),
                .type = try index.type.clone(alloc_),
            };
            try irBuilder_.emit(.{ .lir = .{ .binop = .{
                .dst = index_next,
                .lhs = index,
                .op = .add,
                .rhs = try one.clone(alloc_),
            } } }, alloc_);
            carries[0].next = index_next;
        }
    };

    const body = c.PyObject_GetAttrString(stmt, "body");
    var carries: ArrayList(LoopCarry) = .empty;
    defer carries.deinit(alloc);
    try carries.append(alloc, .{
        .initial = try index0.clone(alloc),
        .current = undefined,
        .next = null,
        .inputs = undefined,
    });

    const len_temp: TypedOperand = .{
        .operand = irBuilder.nextTemp(),
        .type = try index0.type.clone(alloc),
    };

    std.debug.assert(expr.type.isIterable());
    try irBuilder.emit(.{ .len = .{
        .dst = len_temp,
        .value = expr,
    } }, alloc);

    // set for j in jj where type(jj) == array
    const iterator_local: ?LocalId = if (getExprKind(iter) == .Name) blk: {
        const id_obj = c.PyObject_GetAttrString(iter, "id");
        std.debug.assert(id_obj != null);

        const id = c.PyUnicode_AsUTF8(id_obj);
        std.debug.assert(id != null);

        break :blk try irBuilder.getOrCreateLocal(std.mem.span(id), null, alloc);
    } else null;

    try walkLoop(
        irBuilder,
        .{ .operand_compare = .{
            .carry_index = 0,
            .cmp = .lt,
            .rhs = len_temp,
        } },
        .{ .for_loop = .{
            .stmt_list = body,
            .condition_var_name = target_name,
            .iterator = expr,
            .iterator_local = iterator_local,
        } },
        carries.items,
        callback.loop,
        null,
        alloc,
    );
}

// FunctionDef(name='foobar', args=arguments(args=[arg(arg='x'), arg(arg='y')]), body=[Expr(value=Call(func=Name(id='print', ctx=Load()), args=[Name(id='x', ctx=Load())])), Expr(value=Call(func=Name(id='print', ctx=Load()), args=[Name(id='y', ctx=Load())])), Return(value=BinOp(left=Name(id='x', ctx=Load()), op=Add(), right=Name(id='y', ctx=Load())))])
pub fn walkFuncDef(stmt: *PyObject, irBuilder: *IrBuilder, class_id: ?ClassId, alloc: std.mem.Allocator) anyerror!void {
    // start walking function
    const func_name_obj = c.PyObject_GetAttrString(stmt, "name");
    std.debug.assert(func_name_obj != null);
    const raw_func_name = c.PyUnicode_AsUTF8(func_name_obj);
    std.debug.assert(raw_func_name != null);
    const func_name = std.mem.span(raw_func_name);
    const args_obj = c.PyObject_GetAttrString(stmt, "args");
    std.debug.assert(args_obj != null);
    const args_list = c.PyObject_GetAttrString(args_obj, "args");
    std.debug.assert(args_list != null);

    const is_static = try hasDecorator(stmt, "staticmethod");
    // type params (generics)
    var type_params: ArrayList(TypeParam) = .empty;
    errdefer {
        for (type_params.items) |*t_param| {
            t_param.deinit(alloc);
        }
        type_params.deinit(alloc);
    }

    if (class_id) |id| {
        if (!is_static) {
            const class = irBuilder.getClass(id);

            for (class.type_params) |*type_param| {
                try type_params.append(alloc, try type_param.clone(alloc));
            }
        }
    }

    const function_type_params = try parseTypeParams(stmt, @intCast(type_params.items.len), alloc);
    defer alloc.free(function_type_params);
    try type_params.appendSlice(alloc, function_type_params);

    const saved_type_params = irBuilder.active_param_types;
    irBuilder.active_param_types = type_params.items;
    defer irBuilder.active_param_types = saved_type_params;

    // function params
    var params: ArrayList(Param) = .empty;
    errdefer {
        for (params.items) |*param| {
            param.deinit(alloc);
        }
        params.deinit(alloc);
    }
    // iterate through args
    for (0..@intCast(c.PyList_Size(args_list))) |i| {
        const arg_obj = c.PyList_GetItem(args_list, @intCast(i));
        std.debug.assert(arg_obj != null);

        const arg_obj_name = c.PyObject_GetAttrString(arg_obj, "arg");
        std.debug.assert(arg_obj_name != null);
        const annotation = c.PyObject_GetAttrString(arg_obj, "annotation");
        std.debug.assert(annotation != null);

        // only param 0 for classes becomes instance
        const arg_type: TypeInfo = if (class_id == null or is_static or i != 0)
            try parseTypeAnnotation(annotation, irBuilder, alloc)
        else instance: {
            const class = irBuilder.getClass(class_id.?);
            const instance_args = try alloc.alloc(TypeInfo, class.type_params.len);

            for (class.type_params, 0..) |type_param, type_i| {
                instance_args[type_i] = .{
                    .type_variable = type_param.id,
                };
            }
            break :instance .{ .instance = .{
                .class_id = class_id.?,
                .args = instance_args,
            } };
        };
        const raw_name = c.PyUnicode_AsUTF8(arg_obj_name);
        std.debug.assert(raw_name != null);
        const name = std.mem.span(raw_name);

        try params.append(alloc, .{
            .name = try alloc.dupe(u8, name),
            .type = arg_type,
        });
    }
    // default function params
    const default_objs = c.PyObject_GetAttrString(args_obj, "defaults");
    std.debug.assert(default_objs != null);
    const default_len: usize = @intCast(c.PyList_Size(default_objs));
    for (0..default_len) |i| {
        const default_obj = c.PyList_GetItem(default_objs, @intCast(i));
        std.debug.assert(default_obj != null);
        const value_obj = c.PyObject_GetAttrString(default_obj, "value");
        std.debug.assert(value_obj != null);

        const param_index = params.items.len - default_len + i;
        const param = &params.items[param_index];
        params.items[param_index].default = try parseConstant(value_obj, param.type, alloc);
    }

    // return type
    const returns = c.PyObject_GetAttrString(stmt, "returns");
    const return_type = try parseTypeAnnotation(returns, irBuilder, alloc);
    // walk annotation to get function kind
    const kind: FunctionKind = if (try hasDecorator(stmt, "gpu"))
        .gpu_kernel
    else
        .host;
    const is_inline = try hasDecorator(stmt, "inline");

    // append class name onto its methods
    const prefixed_func_name = if (class_id) |id| blk: {
        const class = irBuilder.getClass(id);
        const name = try std.fmt.allocPrint(alloc, "{s}__{s}", .{ class.name, func_name });
        break :blk name;
    } else func_name;
    defer if (class_id != null) alloc.free(prefixed_func_name);

    try irBuilder.program.functions.append(alloc, try Function.init(
        prefixed_func_name,
        irBuilder.nextFunctionId(),
        irBuilder.current_module_id,
        try params.toOwnedSlice(alloc),
        try type_params.toOwnedSlice(alloc),
        return_type,
        irBuilder.function_origin,
        kind,
        is_inline,
        alloc,
    ));

    // save function state
    const saved_current_function = irBuilder.current_function;
    const saved_current_block = irBuilder.current_block;
    var saved_local_values = try irBuilder.cloneLocalValues(alloc);
    defer IrBuilder.deinitLocalValues(&saved_local_values, alloc);

    // set function state
    irBuilder.current_function = irBuilder.program.functions.items.len - 1;
    irBuilder.current_block = 0;
    irBuilder.clearLocalValues(alloc);

    // load function params
    const function = irBuilder.currentFunction();
    for (function.params, 0..) |param, i| {
        const value: TypedOperand = .{
            .operand = irBuilder.nextTemp(),
            .type = try param.type.clone(alloc),
        };

        try irBuilder.emit(.{ .function_param = .{
            .dst = try value.clone(alloc),
            .name = try alloc.dupe(u8, param.name),
            .index = i,
        } }, alloc);

        const local = try irBuilder.getOrCreateLocal(
            param.name,
            param.type,
            alloc,
        );
        try irBuilder.local_values.put(local, value);
    }

    const body = c.PyObject_GetAttrString(stmt, "body");
    std.debug.assert(body != null);
    try walkStmtList(body, irBuilder, alloc);

    // append return if we are missing one
    const block = irBuilder.currentBlock();
    const termianted = block.instructions.items.len > 0 and switch (block.instructions.items[block.instructions.items.len - 1]) {
        .function_return => true,
        .lir => |lir| switch (lir) {
            .jump, .branch => true,
            else => false,
        },
        else => false,
    };
    if (!termianted and function.return_type == .void) {
        try irBuilder.emit(.{ .function_return = .{ .value = null } }, alloc);
    }

    // restore function state
    irBuilder.current_function = saved_current_function;
    irBuilder.current_block = saved_current_block;
    try irBuilder.restoreLocalValues(&saved_local_values, alloc);
}

pub fn hasDecorator(stmt: *PyObject, target_decorator: []const u8) !bool {
    const decorators = PyObject.GetAttrString(stmt, "decorator_list");
    std.debug.assert(decorators != null);
    for (0..@intCast(c.PyList_Size(decorators))) |i| {
        const decorator = c.PyList_GetItem(decorators, @intCast(i));
        std.debug.assert(decorator != null);

        if (!std.mem.eql(u8, getPyType(decorator), "Name")) {
            return error.InvalidDecorator;
        }
        const id_obj = PyObject.GetAttrString(decorator, "id");
        std.debug.assert(id_obj != null);
        const raw_name = c.PyUnicode_AsUTF8(id_obj);
        std.debug.assert(raw_name != null);
        if (std.mem.eql(u8, std.mem.span(raw_name), target_decorator)) {
            return true;
        }
    }
    return false;
}

fn emitResolvedCall(
    function: *const Function,
    callee_name: []const u8,
    arguments: *ArrayList(TypedOperand),
    irBuilder: *IrBuilder,
    alloc: std.mem.Allocator,
) !TypedOperand {
    if (function.kind == .gpu_kernel) {
        if (arguments.items.len != function.params.len + 1) {
            return error.InvalidGpuLaunchArgs;
        }
        const work_item_index = arguments.items.len - 1;
        const work_items = try arguments.items[work_item_index].clone(alloc);
        arguments.items[work_item_index].deinit(alloc);
        arguments.items.len = work_item_index;

        const gpu_args = try arguments.toOwnedSlice(alloc);
        try irBuilder.emit(.{
            .gpu_launch = .{
                .kernel = try alloc.dupe(u8, callee_name),
                .args = gpu_args,
                .work_items = work_items,
            },
        }, alloc);
        return TypedOperand{ .operand = .unknown, .type = .void };
    }
    var bindings: TypeBindings = .init(alloc);
    defer bindings.deinit(alloc);
    const return_type = try bindings.inferReturnType(function, arguments.items, alloc);

    const maybe_dst: ?TypedOperand = if (function.return_type != .void)
        .{
            .operand = irBuilder.nextTemp(),
            .type = return_type,
        }
    else
        null;
    try irBuilder.emit(.{
        .function_call = .{
            .callee = .{ .direct = try alloc.dupe(u8, callee_name) },
            .dst = maybe_dst,
            .args = try arguments.toOwnedSlice(alloc),
        },
    }, alloc);

    if (maybe_dst) |dst| return try dst.clone(alloc);
    return .{ .operand = .unknown, .type = .void };
}

fn parseConstant(
    value_obj: *PyObject,
    expected_type: ?TypeInfo,
    alloc: std.mem.Allocator,
) !ParsedConstant {
    const value_type = getPyType(value_obj);
    if (std.mem.eql(u8, value_type, "int")) {
        const value: ConstValue = .{ .i64 = c.PyLong_AsLong(value_obj) };
        return .{ .immediate = try value.coherce(expected_type) };
    } else if (std.mem.eql(u8, value_type, "float")) {
        const value: ConstValue = .{ .f64 = c.PyFloat_AsDouble(value_obj) };
        return .{ .immediate = try value.coherce(expected_type) };
    } else if (std.mem.eql(u8, value_type, "bool")) {
        const value: ConstValue = .{ .bool = c.PyObject_IsTrue(value_obj) == 1 };
        return .{ .immediate = try value.coherce(expected_type) };
    } else if (std.mem.eql(u8, value_type, "str")) {
        var raw_len: isize = 0;
        const raw = c.PyUnicode_AsUTF8AndSize(value_obj, &raw_len);
        std.debug.assert(raw != null);
        const bytes = raw[0..@intCast(raw_len)];

        if (expected_type) |t| {
            if (t == .char) {
                std.debug.assert(bytes.len == 1);
                return .{ .immediate = .{ .char = bytes[0] } };
            }
        }

        return try makeStringLiteral(bytes, alloc);
    }
    std.debug.print("cant handle {s}\n", .{value_type});
    return error.TypeNotImpl;
}

// Return(value=BinOp(left=Name(id='x', ctx=Load()), op=Add(), right=Name(id='y', ctx=Load())))
// Return()
fn walkReturn(stmt: *PyObject, irBuilder: *IrBuilder, alloc: std.mem.Allocator) !void {
    const value = c.PyObject_GetAttrString(stmt, "value");
    std.debug.assert(value != null);
    const return_top = if (value == c.Py_None())
        null
    else
        try walkExpr(value, irBuilder, null, alloc);

    try irBuilder.emit(.{ .function_return = .{
        .value = return_top,
    } }, alloc);
}

fn makeStringLiteral(bytes: []const u8, alloc: std.mem.Allocator) !ParsedConstant {
    var elements: ArrayList(ValueRef) = .empty;
    // var element_types: ArrayList(TypeInfo) = .empty;
    for (bytes) |char| {
        try elements.append(alloc, .{ .constant = .{
            .char = char,
        } });
        // try element_types.append(alloc, .char);
    }
    // null terminator
    try elements.append(alloc, .{ .constant = .{
        .char = 0,
    } });
    // try element_types.append(alloc, .char);

    const _type: TypeInfo = .{
        .list = .{
            // .elements = try element_types.toOwnedSlice(alloc),
            .element = try TypeInfo.toOwnedPointer(.char, alloc),
            // .size = elements.items.len,
        },
    };

    return .{ .composite = .{
        .elements = try elements.toOwnedSlice(alloc),
        .type = _type,
    } };
}

fn getBinOp(expr: *PyObject) !BinOp {
    const op_obj = c.PyObject_GetAttrString(expr, "op");
    std.debug.assert(op_obj != null);
    const name = getPyType(op_obj);

    if (std.mem.eql(u8, name, "Add")) return .add;
    if (std.mem.eql(u8, name, "Sub")) return .sub;
    if (std.mem.eql(u8, name, "Mult")) return .mul;
    if (std.mem.eql(u8, name, "Div")) return .div;
    if (std.mem.eql(u8, name, "Mod")) return .mod;
    if (std.mem.eql(u8, name, "LShift")) return .lshift;
    if (std.mem.eql(u8, name, "RShift")) return .rshift;
    if (std.mem.eql(u8, name, "MatMult")) return .matmul;

    std.debug.panic("unsupported binop: {s}", .{name});
    return error.NotFound;
}

fn getUnaryOp(expr: *PyObject) !UnaryOp {
    const op_obj = c.PyObject_GetAttrString(expr, "op");
    std.debug.assert(op_obj != null);
    const name = getPyType(op_obj);

    if (std.mem.eql(u8, name, "USub")) return .neg;

    std.debug.panic("unsupported unaryop: {s}", .{name});
    return error.NotFound;
}

fn getCompareOp(expr: *PyObject) !CmpOp {
    const ops = c.PyObject_GetAttrString(expr, "ops");
    std.debug.assert(ops != null);
    const ops_obj = c.PyList_GetItem(ops, 0);
    std.debug.assert(ops_obj != null);

    const name = getPyType(ops_obj);

    if (std.mem.eql(u8, name, "Eq")) return .eq;
    if (std.mem.eql(u8, name, "NotEq")) return .neq;
    if (std.mem.eql(u8, name, "Lt")) return .lt;
    if (std.mem.eql(u8, name, "LtE")) return .lte;
    if (std.mem.eql(u8, name, "Gt")) return .gt;
    if (std.mem.eql(u8, name, "GtE")) return .gte;

    std.debug.panic("unsupported compare op: {s}", .{name});
    return error.NotFound;
}

fn getExprKind(stmt: *PyObject) ExprKind {
    const name = getPyType(stmt);
    if (std.mem.eql(u8, name, "BinOp")) return .BinOp;
    if (std.mem.eql(u8, name, "Compare")) return .Compare;
    if (std.mem.eql(u8, name, "UnaryOp")) return .UnaryOp;
    if (std.mem.eql(u8, name, "Constant")) return .Constant;
    if (std.mem.eql(u8, name, "Name")) return .Name;
    if (std.mem.eql(u8, name, "Call")) return .Call;
    if (std.mem.eql(u8, name, "List")) return .List;
    if (std.mem.eql(u8, name, "Tuple")) return .Tuple;
    if (std.mem.eql(u8, name, "Subscript")) return .Subscript;
    if (std.mem.eql(u8, name, "IfExp")) return .IfExp;
    if (std.mem.eql(u8, name, "Attribute")) return .Attribute;

    return .Unknown;
}

fn parseTypeAnnotation(
    annotation: *PyObject,
    irBuilder: *IrBuilder,
    alloc: std.mem.Allocator,
) !TypeInfo {
    const kind = getPyType(annotation);
    // Name(id='int', ctx=Load())
    if (std.mem.eql(u8, kind, "Name")) {
        const annotation_id_obj = c.PyObject_GetAttrString(annotation, "id");
        std.debug.assert(annotation_id_obj != null);
        const annotation_id = c.PyUnicode_AsUTF8(annotation_id_obj);
        std.debug.assert(annotation_id != null);

        const annotation_name = std.mem.span(annotation_id);
        if (std.mem.eql(u8, annotation_name, "int")) {
            return .i64;
        } else if (std.mem.eql(u8, annotation_name, "i32")) {
            return .i32;
        } else if (std.mem.eql(u8, annotation_name, "bool")) {
            return .bool;
        } else if (std.mem.eql(u8, annotation_name, "float")) {
            return .f64;
        } else if (std.mem.eql(u8, annotation_name, "f32")) {
            return .f32;
        } else if (std.mem.eql(u8, annotation_name, "char")) {
            return .char;
        } else if (std.mem.eql(u8, annotation_name, "str")) {
            return .{ .list = .{ .element = try TypeInfo.toOwnedPointer(.char, alloc) } };
        } else if (irBuilder.getActiveParmType(annotation_name)) |param_type| {
            return .{ .type_variable = param_type.id };
        } else if (irBuilder.findClass(annotation_name)) |class| {
            if (class.type_params.len != 0) return error.InvalidTypeArgCount;
            return .{ .instance = .{
                .class_id = class.id,
                .args = try alloc.alloc(TypeInfo, 0),
            } };
        } else if (irBuilder.currentFunction().findTypeParam(annotation_name)) |type_param| {
            return .{
                .type_variable = type_param.id,
            };
        }
        std.debug.print("cant handle {s}\n", .{annotation_id});
        return error.TypeNotImplemented;
    } else if (std.mem.eql(u8, kind, "Subscript")) {
        const slice_obj = c.PyObject_GetAttrString(annotation, "slice");
        std.debug.assert(slice_obj != null);

        switch (try getSubscriberType(annotation, irBuilder)) {
            // Subscript(value=Name(id='list', ctx=Load()), slice=Name(id='int', ctx=Load()), ctx=Load())
            .list => {
                // recursively get type
                const elem_type = try parseTypeAnnotation(slice_obj, irBuilder, alloc);
                return .{ .list = .{
                    .element = try elem_type.toOwnedPointer(alloc),
                } };
            },
            // Subscript(value=Name(id='tuple', ctx=Load()), slice=Tuple(elts=[Name(id='int', ctx=Load()), Name(id='int', ctx=Load())], ctx=Load()), ctx=Load())
            .tuple => {
                const elts = c.PyObject_GetAttrString(slice_obj, "elts");
                std.debug.assert(elts != null);
                const len: usize = @intCast(c.PyList_Size(elts));
                const elem_types = try alloc.alloc(TypeInfo, len);
                for (0..len) |i| {
                    const elt = c.PyList_GetItem(elts, @intCast(i));
                    std.debug.assert(elt != null);
                    elem_types[i] = try parseTypeAnnotation(elt, irBuilder, alloc);
                }
                return .{ .tuple = .{
                    .elements = elem_types,
                } };
            },
            // Subscript(value=Name(id='Callable', ctx=Load()), slice=Tuple(elts=[List(elts=[Name(id='bool', ctx=Load())], ctx=Load()), Name(id='int', ctx=Load())], ctx=Load()), ctx=Load())
            .callable => {
                const elts = c.PyObject_GetAttrString(slice_obj, "elts");
                std.debug.assert(elts != null);
                const len: usize = @intCast(c.PyList_Size(elts));
                std.debug.assert(len == 2);
                const params_obj = c.PyList_GetItem(elts, 0);
                std.debug.assert(params_obj != null);
                const return_obj = c.PyList_GetItem(elts, 1);
                std.debug.assert(return_obj != null);

                const params_elts = c.PyObject_GetAttrString(params_obj, "elts");
                std.debug.assert(params_elts != null);
                const input_len: usize = @intCast(c.PyList_Size(params_elts));
                const elem_types = try alloc.alloc(TypeInfo, input_len);
                for (0..input_len) |i| {
                    const elt = c.PyList_GetItem(params_elts, @intCast(i));
                    std.debug.assert(elt != null);
                    elem_types[i] = try parseTypeAnnotation(elt, irBuilder, alloc);
                }

                return .{ .callable = .{
                    .params = elem_types,
                    .returns = try (try parseTypeAnnotation(return_obj, irBuilder, alloc)).toOwnedPointer(alloc),
                } };
            },
            .instance => |class_id| {
                const class = irBuilder.getClass(class_id);
                const arity = class.type_params.len;

                if (arity == 0) return error.InvalidTypeArgCount;

                const args = try alloc.alloc(TypeInfo, arity);

                // arity = 1 is a special case
                // more args get pushed into elts
                if (arity == 1) {
                    args[0] = try parseTypeAnnotation(slice_obj, irBuilder, alloc);
                } else {
                    return error.NotImpl;
                }

                return .{ .instance = .{
                    .class_id = class_id,
                    .args = args,
                } };
            },
        }
    } else if (std.mem.eql(u8, kind, "Constant")) {
        const value_obj = c.PyObject_GetAttrString(annotation, "value");
        if (value_obj == c.Py_None()) {
            return .void;
        }
        if (!std.mem.eql(u8, getPyType(value_obj), "str")) {
            return error.ExpectedString;
        }
        // classes support string type lookup
        const raw_class_name = c.PyUnicode_AsUTF8(value_obj);
        const class = irBuilder.findClass(std.mem.span(raw_class_name)) orelse {
            return error.CantFindClass;
        };
        return .{ .instance = .{
            .class_id = class.id,
            .args = try alloc.alloc(TypeInfo, 0),
        } };
    }
    std.debug.print("kind not supported {s}\n", .{kind});
    return error.NotImpl;
}

// ClassDef(name='MiniTorch', type_params=[ TypeVar(name='T')], body=[...])
fn parseTypeParams(stmt: *PyObject, start_id: u32, alloc: std.mem.Allocator) ![]TypeParam {
    const type_params = PyObject.GetAttrString(stmt, "type_params");
    std.debug.assert(type_params != null);

    const type_params_size: usize = @intCast(c.PyList_Size(type_params));
    const result = try alloc.alloc(TypeParam, type_params_size);
    for (0..type_params_size) |i| {
        const type_param = c.PyList_GetItem(type_params, @intCast(i));
        std.debug.assert(type_param != null);
        const name_obj = PyObject.GetAttrString(type_param, "name");
        std.debug.assert(name_obj != null);
        const name = PyObject.PyUnicode_AsUTF8(name_obj);
        std.debug.assert(name != null);

        result[i] = .{
            .name = try alloc.dupe(u8, std.mem.span(name)),
            .id = start_id + @as(@FieldType(TypeParam, "id"), @intCast(i)),
        };
    }
    return result;
}

fn getSubscriberType(annotation: *PyObject, irBuilder: *IrBuilder) !SubscriberTypes {
    const value_obj = c.PyObject_GetAttrString(annotation, "value");
    std.debug.assert(value_obj != null);
    const id_obj = c.PyObject_GetAttrString(value_obj, "id");
    std.debug.assert(id_obj != null);
    const name = std.mem.span(c.PyUnicode_AsUTF8(id_obj));
    if (std.mem.eql(u8, name, "list")) return .list;
    if (std.mem.eql(u8, name, "tuple")) return .tuple;
    if (std.mem.eql(u8, name, "Callable")) return .callable;
    if (irBuilder.findClass(name)) |class| {
        return .{ .instance = class.id };
    }

    return error.InvalidSubscriber;
}

fn getBuiltinCall(name: []const u8) ?BuiltinCall {
    if (std.mem.eql(u8, name, "range")) {
        return BuiltinCall.Range;
    } else if (std.mem.eql(u8, name, "print")) {
        return BuiltinCall.Print;
    } else if (std.mem.eql(u8, name, "write")) {
        return BuiltinCall.Write;
    } else if (std.mem.eql(u8, name, "len")) {
        return BuiltinCall.Len;
    } else if (std.mem.eql(u8, name, "int")) {
        return BuiltinCall.Int;
    } else if (std.mem.eql(u8, name, "i32")) {
        return BuiltinCall.I32;
    } else if (std.mem.eql(u8, name, "float")) {
        return BuiltinCall.Float;
    } else if (std.mem.eql(u8, name, "global_id")) {
        return BuiltinCall.GlobalIdx;
    } else if (std.mem.eql(u8, name, "max")) {
        return BuiltinCall.Max;
    } else if (std.mem.eql(u8, name, "exp")) {
        return BuiltinCall.Exp;
    } else if (std.mem.eql(u8, name, "exp2")) {
        return BuiltinCall.Exp2;
    } else if (std.mem.eql(u8, name, "type")) {
        return BuiltinCall.Type;
    }
    return null;
}

test "while loop" {
    c.Py_Initialize();
    defer _ = c.Py_FinalizeEx();

    const alloc = std.testing.allocator;
    const code: [*:0]const u8 =
        \\x = 0
        \\while x < 3:
        \\  x = x + 1
        \\  print(x)
        \\print(x)
    ;

    const ast_module = c.PyImport_ImportModule("ast");
    const parse_fn = c.PyObject_GetAttrString(ast_module, "parse");
    const tree = c.PyObject_CallFunction(parse_fn, "s", code);
    std.debug.assert(tree != null);

    var irBuilder = try IrBuilder.init(.user, alloc);
    defer irBuilder.deinit(alloc);
    errdefer irBuilder.program.deinit(alloc);
    try walkAstIntoBuilder(tree, &irBuilder, alloc);
    var program = irBuilder.program;
    defer program.deinit(alloc);

    try std.testing.expectEqual(@as(usize, 4), program.main.blocks.items.len);

    const entry = program.main.blocks.items[0].instructions.items;
    const condition = program.main.blocks.items[1].instructions.items;
    const body = program.main.blocks.items[2].instructions.items;
    const exit = program.main.blocks.items[3].instructions.items;

    try std.testing.expectEqualDeep(
        Instruction{ .lir = .{ .move = .{
            .dst = .{ .operand = .{ .temp = .{ .id = 0, .function_id = 0 } }, .type = .i64 },
            .src = .{ .constant = .{ .i64 = 0 } },
        } } },
        entry[0],
    );
    try std.testing.expectEqualDeep(
        Instruction{
            .lir = .{ .store_local = .{ .local = .{
                .id = 0,
                .name = "x",
                .type = .i64,
            }, .src = .{
                .operand = .{ .temp = .{ .id = 0, .function_id = 0 } },
                .type = .i64,
            } } },
        },
        entry[1],
    );
    try std.testing.expectEqualDeep(
        Instruction{ .lir = .{ .jump = .{ .target = 1 } } },
        entry[2],
    );

    switch (condition[0]) {
        .phi => {
            // temp1 = phi(entry: temp0, body: temp4)
        },
        else => return error.ExpectedPhi,
    }

    try std.testing.expectEqual(.lir, std.meta.activeTag(body[1]));
    switch (body[1].lir) {
        .binop => {},
        else => return error.ExpectedBinOp,
    }

    switch (exit[0]) {
        .print => {},
        else => return error.ExpectedPrint,
    }
}
