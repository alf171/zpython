const std = @import("std");
const HashMap = std.AutoHashMap;
const Operand = @import("common").alloc.Operand;
const SubscriptStore = @import("common").mir.SubscriptStore;
const ValueRef = @import("common").ir.ValueRef;
const TypedOperand = @import("common").alloc.TypedOperand;
const Function = @import("common").ir.Function;
const Program = @import("common").program.Program;
const Instruction = @import("common").mir.Instruction;

/// calls malloc and handles layoff buisness logic like size being the first elem
pub fn rewrite(program: *Program, alloc: std.mem.Allocator) !void {
    try rewriteFunction(&program.main, alloc);
    for (program.functions.items) |*function| {
        try rewriteFunction(function, alloc);
    }
}

fn rewriteFunction(function: *Function, alloc: std.mem.Allocator) !void {
    for (function.blocks.items) |*block| {
        var new_instructions = std.ArrayList(Instruction).empty;
        errdefer new_instructions.deinit(alloc);

        for (block.instructions.items) |*instruction| {
            switch (instruction.*) {
                .list_literal => |ll| {
                    const elem_type = try ll.dst.type.getElementType();
                    const byte_count = 8 + ll.elements.len * try elem_type.sizeOfType();
                    const byte_count_ref: ValueRef = .{ .constant = .{ .i64 = @intCast(byte_count) } };
                    const list_length_ref: ValueRef = .{ .constant = .{ .i64 = @intCast(ll.elements.len) } };
                    try lowerListAlloc(
                        function,
                        ll.dst,
                        byte_count_ref,
                        list_length_ref,
                        &new_instructions,
                        alloc,
                    );
                    // store elements
                    for (ll.elements, 0..) |elem, i| {
                        const src: ValueRef = switch (elem) {
                            .constant => |c| blk: {
                                break :blk .{ .constant = c };
                            },
                            .top => |top| blk: {
                                const src: TypedOperand = .{
                                    .operand = function.nextTemp(),
                                    .type = try top.type.clone(alloc),
                                };
                                try new_instructions.append(alloc, .{ .lir = .{ .move = .{
                                    .dst = src,
                                    .src = .{ .top = try top.clone(alloc) },
                                } } });
                                break :blk .{ .top = src };
                            },
                        };
                        const index: TypedOperand = .{
                            .operand = function.nextTemp(),
                            .type = .i64,
                        };
                        try new_instructions.append(alloc, .{ .lir = .{ .move = .{
                            .dst = index,
                            .src = .{ .constant = .{ .i64 = @intCast(i) } },
                        } } });
                        try rewriteListStore(function, .{
                            .target = ll.dst,
                            .index = index,
                            .src = src,
                        }, &new_instructions, alloc);
                    }
                    instruction.deinit(alloc);
                },
                .subscript_store => |ss| {
                    if (ss.target.type != .list) {
                        try new_instructions.append(alloc, instruction.*);
                        continue;
                    }
                    try rewriteListStore(function, ss, &new_instructions, alloc);
                    instruction.deinit(alloc);
                },
                .subscript => |s| {
                    if (s.src.type != .list) {
                        try new_instructions.append(alloc, instruction.*);
                        continue;
                    }

                    // dst <- list[index]
                    const scaled: TypedOperand = .{ .operand = function.nextTemp(), .type = .i64 };
                    const offset: TypedOperand = .{ .operand = function.nextTemp(), .type = .i64 };
                    const elem_type = try s.src.type.getElementType();
                    const elem_size = try elem_type.sizeOfType();
                    // scaled = index
                    if (elem_size == 1) {
                        try new_instructions.append(alloc, .{ .lir = .{ .move = .{
                            .dst = scaled,
                            .src = .{ .top = try s.index.clone(alloc) },
                        } } });
                    }
                    // scaled = index * element_size
                    else {
                        const element_size: TypedOperand = .{ .operand = function.nextTemp(), .type = .i64 };
                        try new_instructions.append(alloc, .{ .lir = .{ .move = .{
                            .dst = element_size,
                            .src = .{ .constant = .{ .i64 = @intCast(elem_size) } },
                        } } });
                        try new_instructions.append(alloc, .{ .lir = .{ .binop = .{
                            .dst = scaled,
                            .op = .mul,
                            .lhs = try s.index.clone(alloc),
                            .rhs = element_size,
                        } } });
                    }
                    // offset = scaled + 8
                    const eight: TypedOperand = .{ .operand = function.nextTemp(), .type = .i64 };
                    try new_instructions.append(alloc, .{ .lir = .{ .move = .{
                        .dst = eight,
                        .src = .{ .constant = .{ .i64 = 8 } },
                    } } });
                    try new_instructions.append(alloc, .{ .lir = .{ .binop = .{
                        .dst = offset,
                        .op = .add,
                        .lhs = scaled,
                        .rhs = eight,
                    } } });
                    try new_instructions.append(alloc, .{ .lir = .{
                        .load_offset = .{
                            .dst = try s.dst.clone(alloc),
                            .src = try s.src.clone(alloc),
                            .offset = .{ .top = try offset.clone(alloc) },
                        },
                    } });
                    instruction.deinit(alloc);
                },
                else => try new_instructions.append(alloc, instruction.*),
            }
        }
        block.instructions.deinit(alloc);
        block.instructions = new_instructions;
    }
}

/// calls malloc and stores size at index 0
fn lowerListAlloc(
    function: *Function,
    dst: TypedOperand,
    byte_count: ValueRef,
    list_length: ValueRef,
    new_instructions: *std.ArrayList(Instruction),
    alloc: std.mem.Allocator,
) !void {
    const size_temp = function.nextTemp();
    try new_instructions.append(alloc, .{ .lir = .{ .move = .{
        .dst = .{ .operand = size_temp, .type = .i64 },
        .src = byte_count,
    } } });
    const args = try alloc.dupe(TypedOperand, &.{
        .{ .operand = size_temp, .type = .i64 },
    });
    try new_instructions.append(alloc, .{ .function_call = .{
        .dst = try dst.clone(alloc),
        .callee = .{
            .direct = try alloc.dupe(u8, "arena_malloc"),
        },
        .args = args,
    } });
    // store list size
    {
        const src = function.nextTemp();
        try new_instructions.append(alloc, .{ .lir = .{ .move = .{
            .dst = .{ .operand = src, .type = .i64 },
            .src = list_length,
        } } });
        try new_instructions.append(alloc, .{
            .lir = .{ .store_offset = .{
                .dst = try dst.clone(alloc),
                .offset = .{ .constant = .{ .i64 = 0 } },
                .src = .{ .operand = src, .type = .i64 },
            } },
        });
    }
}

fn rewriteListStore(
    function: *Function,
    ss: SubscriptStore,
    new_instructions: *std.ArrayList(Instruction),
    alloc: std.mem.Allocator,
) !void {
    const scaled: TypedOperand = .{ .operand = function.nextTemp(), .type = .i64 };
    const offset: TypedOperand = .{ .operand = function.nextTemp(), .type = .i64 };
    const elem_type = try ss.target.type.getElementType();
    const elem_size = try elem_type.sizeOfType();
    // scaled = index
    if (elem_size == 1) {
        try new_instructions.append(alloc, .{ .lir = .{ .move = .{
            .dst = scaled,
            .src = .{ .top = ss.index },
        } } });
    }
    // scaled = index * element_size
    else {
        const element_size: TypedOperand = .{ .operand = function.nextTemp(), .type = .i64 };
        try new_instructions.append(alloc, .{ .lir = .{ .move = .{
            .dst = element_size,
            .src = .{ .constant = .{ .i64 = @intCast(elem_size) } },
        } } });
        try new_instructions.append(alloc, .{ .lir = .{ .binop = .{
            .dst = scaled,
            .op = .mul,
            .lhs = ss.index,
            .rhs = element_size,
        } } });
    }
    // offset = scaled + 8
    const eight: TypedOperand = .{ .operand = function.nextTemp(), .type = .i64 };
    try new_instructions.append(alloc, .{ .lir = .{ .move = .{
        .dst = eight,
        .src = .{ .constant = .{ .i64 = 8 } },
    } } });
    try new_instructions.append(alloc, .{ .lir = .{ .binop = .{
        .dst = offset,
        .op = .add,
        .lhs = scaled,
        .rhs = eight,
    } } });

    const src: TypedOperand = switch (ss.src) {
        .top => |top| .{
            .operand = top.operand,
            .type = try elem_type.clone(alloc),
        },
        .constant => |c| blk: {
            const tmp = function.nextTemp();
            try new_instructions.append(alloc, .{ .lir = .{ .move = .{
                .dst = .{ .operand = tmp, .type = c.toType() },
                .src = .{ .constant = c },
            } } });
            break :blk .{
                .operand = tmp,
                .type = try elem_type.clone(alloc),
            };
        },
    };

    try new_instructions.append(alloc, .{ .lir = .{ .store_offset = .{
        .dst = try ss.target.clone(alloc),
        .offset = .{ .top = try offset.clone(alloc) },
        .src = src,
    } } });
}
