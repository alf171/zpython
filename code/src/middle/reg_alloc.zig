const std = @import("std");
const ArrayList = std.ArrayList;

const common = @import("common");
const LocalId = common.ir.LocalId;
const AllocProgram = common.alloc.AllocProgram;
const AllocBlock = common.alloc.AllocBlock;
const AllocLine = common.alloc.AllocLine;
const RegisterOperands = common.alloc.RegisterOperands;
const Function = common.ir.Function;
const FunctionKind = common.ir.FunctionKind;
const Program = common.program.Program;
const TypedOperand = common.alloc.TypedOperand;
const RegisterClasses = @import("common").register.RegisterClasses;

/// generate the necessary information such that we do register selection eventually
pub fn build(program: Program, reg_classes: *const RegisterClasses, alloc: std.mem.Allocator) !AllocProgram {
    var res: AllocProgram = .{
        .lines = .empty,
        .blocks = .empty,
    };
    errdefer res.deinit(alloc);

    var instruction_index: usize = 0;
    for (program.functions.items) |function| {
        try appendBlocks(
            function.blocks.items,
            &res,
            &instruction_index,
            function.id,
            reg_classes,
            alloc,
        );
    }
    try appendBlocks(
        program.main.blocks.items,
        &res,
        &instruction_index,
        0,
        reg_classes,
        alloc,
    );

    return res;
}

fn appendBlocks(
    blocks: []const common.ir.BasicBlock,
    res: *AllocProgram,
    instruction_index: *usize,
    function_id: usize,
    reg_classes: *const RegisterClasses,
    alloc: std.mem.Allocator,
) !void {
    var locals = std.AutoHashMap(LocalId, TypedOperand).init(alloc);
    defer locals.deinit();

    const block_offset = res.blocks.items.len;
    for (blocks, 0..) |block, i| {
        std.debug.assert(block.id == @as(common.ir.BlockId, @intCast(i)));
        const start = res.lines.items.len;
        for (block.instructions.items) |instruction| {
            var line: AllocLine = .{
                .instruction_index = instruction_index.*,
                .uses = RegisterOperands.init(alloc),
                .defines = RegisterOperands.init(alloc),
                .live_out = RegisterOperands.init(alloc),
                .move = false,
                .clobber_caller_saved = false,
            };
            errdefer line.deinit();
            // set move flag and store locals for later use
            switch (instruction) {
                .function_call => line.clobber_caller_saved = true,
                .lir => |lir| {
                    switch (lir) {
                        .store_local => |sl| {
                            try locals.put(sl.local.id, sl.src);
                        },
                        .move => |m| line.move = m.src == .top,
                        .load_local => line.move = true,
                        else => {},
                    }
                },
                else => {},
            }
            // get defines
            const maybeDefines = instruction.getDefines();
            if (maybeDefines) |defines| {
                switch (defines) {
                    .top => |top| try line.defines.ops.put(
                        top.operand,
                        try reg_classes.get(top.operand),
                    ),
                    .local => {},
                }
            }
            // get uses
            var uses = try instruction.getUses(alloc);
            defer uses.deinit(alloc);
            for (uses.items) |use| {
                switch (use) {
                    .top => |top| {
                        const reg = reg_classes.get(top.operand) catch |err| {
                            std.debug.print(
                                "missing register class: operand={any}, function_id={d}\n",
                                .{ top.operand, function_id },
                            );
                            try instruction.printFn();
                            return err;
                        };
                        try line.uses.ops.put(top.operand, reg);
                    },
                    .local => |id| {
                        const src = locals.get(id) orelse {
                            switch (instruction) {
                                .lir => |lir| switch (lir) {
                                    .load_local => |ll| {
                                        std.debug.print("cant find local \"{s}\"\n", .{ll.local.name});
                                        return error.LocalNotFound;
                                    },
                                    else => return error.LocalNotFound,
                                },
                                else => return error.LocalNotFound,
                            }
                        };
                        try line.uses.ops.put(src.operand, try reg_classes.get(src.operand));
                    },
                }
            }

            try res.lines.append(alloc, line);
            instruction_index.* += 1;
        }
        const end = res.lines.items.len;
        // TODO: move predecessors and successors transfer into BB?
        var predecessors: ArrayList(u32) = .empty;
        errdefer predecessors.deinit(alloc);
        for (block.predecessors.items) |block_id| {
            const idx = block_offset + block_id;

            try predecessors.append(alloc, @intCast(idx));
        }
        var successors: ArrayList(u32) = .empty;
        errdefer successors.deinit(alloc);
        for (block.successors.items) |block_id| {
            const idx = block_offset + block_id;

            try successors.append(alloc, @intCast(idx));
        }

        try res.blocks.append(alloc, .{
            .id = block.id,
            .start = start,
            .end = end,
            .successors = successors,
            .predecessors = predecessors,
            .function_id = function_id,
        });
    }
}

test "lower" {
    try std.testing.expectEqual(true, true);
}
