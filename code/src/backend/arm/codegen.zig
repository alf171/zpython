const std = @import("std");
const ArrayList = std.ArrayList;

const common = @import("common");
const Program = common.program.Program;
const TypeInfo = common.types.TypeInfo;
const Block = common.ir.BasicBlock;
const ConstValue = common.ir.ConstValue;
const Function = common.ir.Function;
const ValueRef = common.ir.ValueRef;
const ColoredGraph = @import("middle").color.ColoredGraph;
const Abi = @import("../cpu_abi.zig").CpuAbi;
const RegisterType = @import("common").ir.RegisterType;

pub fn emit(program: *const Program, colors: *const ColoredGraph, abi: Abi, alloc: std.mem.Allocator) ![]u8 {
    var out = ArrayList(u8).empty;
    errdefer out.deinit(alloc);

    try createProgramHeader(&out, alloc);
    std.debug.assert(program.main.kind == .host);
    try emitFunction(&out, colors, &program.main, abi, true, alloc);
    for (program.functions.items) |function| {
        if (function.kind != .host) continue;
        try emitFunction(&out, colors, &function, abi, false, alloc);
    }

    try createFooter(&out, alloc);

    return out.toOwnedSlice(alloc);
}

fn emitFunction(
    out: *ArrayList(u8),
    colors: *const ColoredGraph,
    function: *const Function,
    abi: Abi,
    is_main: bool,
    alloc: std.mem.Allocator,
) !void {
    // emit comment used by metrics for function origin metrics
    try out.print(alloc, "// origin: {s}\n", .{@tagName(function.origin)});
    const local_count = countLocals(&function.blocks);
    const stack_bytes = countStackAllocBytes(&function.blocks);
    const local_stack_size = std.mem.alignForward(
        usize,
        stack_bytes + (local_count * 8),
        16,
    );
    const frame_stack_size = std.mem.alignForward(
        usize,
        local_stack_size + (function.next_mem * 8),
        16,
    );
    try createFunctionHeader(out, function.label, frame_stack_size, abi, alloc);
    var next_stack_alloc_byte: usize = 0;
    for (function.blocks.items) |block| {
        try out.print(alloc, "_{s}_L{d}:\n", .{ function.label, block.id });
        for (block.instructions.items) |instruction| {
            // TODO: this method should only be look at LIR. having MIR here is a hack!
            switch (instruction) {
                .lir => |l| {
                    switch (l) {
                        // str: src, dst (register -> memory)
                        .store_local => |sl| {
                            const src = try abi.regFor(sl.src.operand, colors);
                            try emitStackStore(out, src, localOffset(sl.local.id), try abi.scratchReg(0, .gp), alloc);
                        },
                        .store_offset => |so| {
                            const dst = try abi.regFor(so.dst.operand, colors);
                            const src = try abi.regFor(so.src.operand, colors);
                            switch (so.offset) {
                                .constant => |c| switch (c) {
                                    .i64 => |offset| {
                                        try out.print(alloc, "\tstr {s}, [{s}, #{d}]\n", .{ src, dst, offset });
                                    },
                                    else => return error.NotImpl,
                                },
                                .top => |top| {
                                    std.debug.assert(top.type == .i64);
                                    const offset = try abi.regFor(top.operand, colors);
                                    switch (so.src.type) {
                                        .i64, .list => {
                                            try out.print(alloc, "\tstr {s}, [{s}, {s}]\n", .{ src, dst, offset });
                                        },
                                        .i32 => {
                                            std.debug.assert(src[0] == 'x');
                                            try out.print(alloc, "\tstr w{s}, [{s}, {s}]\n", .{ src[1..], dst, offset });
                                        },
                                        .char, .bool => {
                                            std.debug.assert(src[0] == 'x');
                                            try out.print(alloc, "\tstrb w{s}, [{s}, {s}]\n", .{ src[1..], dst, offset });
                                        },
                                        else => |e| {
                                            std.debug.print("cant handle {s}\n", .{@tagName(e)});
                                            return error.NotImpl;
                                        },
                                    }
                                },
                            }
                        },
                        .load_offset => |lo| {
                            const dst = try abi.regFor(lo.dst.operand, colors);
                            const src = try abi.regFor(lo.src.operand, colors);
                            switch (lo.offset) {
                                .constant => |c| switch (c) {
                                    .i64 => |offset| {
                                        try out.print(alloc, "\tldr {s}, [{s}, #{d}]\n", .{ dst, src, offset });
                                    },
                                    else => return error.NotImpl,
                                },
                                .top => |top| {
                                    const offset = try abi.regFor(top.operand, colors);
                                    switch (lo.dst.type) {
                                        .i32 => {
                                            try out.print(alloc, "\tldrsw {s}, [{s}, {s}]\n", .{ dst, src, offset });
                                        },
                                        else => {
                                            try out.print(alloc, "\tldr {s}, [{s}, {s}]\n", .{ dst, src, offset });
                                        },
                                    }
                                },
                            }
                        },
                        // ldr: dst, src (memory -> register)
                        .load_local => |ll| {
                            const dst = try abi.regFor(ll.dst.operand, colors);
                            try emitStackLoad(out, dst, localOffset(ll.local.id), try abi.scratchReg(0, .gp), alloc);
                        },
                        .move => |m| {
                            switch (m.src) {
                                .constant => |c| {
                                    switch (c) {
                                        .i64, .i32 => |value| {
                                            const dst = try abi.regFor(m.dst.operand, colors);
                                            try emitMov(out, dst, value, alloc);
                                        },
                                        .bool => |value| {
                                            const dst = try abi.regFor(m.dst.operand, colors);
                                            try out.print(alloc, "\tmov {s}, #{d}\n", .{ dst, @intFromBool(value) });
                                        },
                                        .char => |value| {
                                            const dst = try abi.regFor(m.dst.operand, colors);
                                            try out.print(alloc, "\tmov {s}, #{d}\n", .{ dst, value });
                                        },
                                        .f64 => |value| {
                                            const dst = try abi.regFor(m.dst.operand, colors);
                                            const bits: u64 = @bitCast(value);
                                            const scratch_reg = try abi.scratchReg(0, .gp);
                                            try emitMovUnsigned(out, scratch_reg, bits, alloc);
                                            try out.print(alloc, "\tfmov {s}, {s}\n", .{ dst, scratch_reg });
                                        },
                                        else => return error.NotImpl,
                                    }
                                },
                                .top => |src_top| {
                                    switch (m.dst.operand) {
                                        .temp => {
                                            const dst = try abi.regFor(m.dst.operand, colors);
                                            switch (src_top.operand) {
                                                // temp <- temp
                                                .temp => {
                                                    const src = try abi.regFor(src_top.operand, colors);
                                                    if (std.mem.eql(u8, dst, src)) continue;
                                                    switch (m.dst.type) {
                                                        .f64 => try out.print(alloc, "\tfmov {s}, {s}\n", .{ dst, src }),
                                                        else => try out.print(alloc, "\tmov {s}, {s}\n", .{ dst, src }),
                                                    }
                                                },
                                                // reg <- temp
                                                .reg => |reg| {
                                                    switch (reg.type) {
                                                        .f => try out.print(alloc, "\tfmov ", .{}),
                                                        .gp => try out.print(alloc, "\tmov ", .{}),
                                                        else => unreachable,
                                                    }
                                                    const src = try abi.regForFromIndex(reg.id, reg.type);
                                                    try out.print(alloc, "{s}, {s}\n", .{ dst, src });
                                                },
                                                // temp <- mem
                                                .mem => |slot| {
                                                    const offset = spillOffset(local_stack_size, slot.id);
                                                    try emitStackLoad(out, dst, offset, try abi.scratchReg(0, .gp), alloc);
                                                },
                                                .unknown => return error.UnexpectedState,
                                            }
                                        },
                                        .mem => |slot| {
                                            switch (src_top.operand) {
                                                // mem <- reg
                                                .temp => {
                                                    const offset = spillOffset(local_stack_size, slot.id);
                                                    const src = try abi.regFor(src_top.operand, colors);
                                                    try emitStackStore(out, src, offset, try abi.scratchReg(0, .gp), alloc);
                                                },
                                                .mem => {
                                                    return error.MemoryToMemoryMoveDetected;
                                                },
                                                else => return error.NotImpl,
                                            }
                                        },
                                        .reg => |reg| {
                                            switch (src_top.operand) {
                                                // reg <- temp
                                                .temp => {
                                                    const dst = try abi.regFor(m.dst.operand, colors);
                                                    const src = try abi.regFor(src_top.operand, colors);
                                                    switch (reg.type) {
                                                        .f => {
                                                            try out.print(alloc, "\tfmov {s}, {s}\n", .{ dst, src });
                                                        },
                                                        .gp => {
                                                            try out.print(alloc, "\tmov {s}, {s}\n", .{ dst, src });
                                                        },
                                                        else => unreachable,
                                                    }
                                                },
                                                // reg <- reg
                                                .reg => |src_reg| {
                                                    const dst = try abi.regForFromIndex(reg.id, reg.type);
                                                    const src = try abi.regForFromIndex(src_reg.id, src_reg.type);
                                                    switch (reg.type) {
                                                        .f => {
                                                            try out.print(alloc, "\tfmov {s}, {s}\n", .{ dst, src });
                                                        },
                                                        .gp => {
                                                            try out.print(alloc, "\tmov {s}, {s}\n", .{ dst, src });
                                                        },
                                                        else => unreachable,
                                                    }
                                                },
                                                else => |e| {
                                                    std.debug.print("{s} not impl!\n", .{@tagName(e)});
                                                    return error.NotImpl;
                                                },
                                            }
                                        },
                                        .unknown => return error.UnexpectedState,
                                    }
                                },
                            }
                        },
                        .binop => |binop| {
                            const dst = try abi.regFor(binop.dst.operand, colors);
                            const lhs = try abi.regFor(binop.lhs.operand, colors);
                            const rhs = try abi.regFor(binop.rhs.operand, colors);

                            switch (binop.op) {
                                .add => {
                                    switch (binop.dst.type) {
                                        .f64, .f32 => try out.print(alloc, "\tfadd ", .{}),
                                        else => try out.print(alloc, "\tadd ", .{}),
                                    }
                                    try out.print(alloc, "{s}, {s}, {s}\n", .{ dst, lhs, rhs });
                                },
                                .sub => {
                                    switch (binop.dst.type) {
                                        .f64, .f32 => try out.print(alloc, "\tfsub ", .{}),
                                        else => try out.print(alloc, "\tsub ", .{}),
                                    }
                                    try out.print(alloc, "{s}, {s}, {s}\n", .{ dst, lhs, rhs });
                                },
                                .mul => {
                                    switch (binop.dst.type) {
                                        .f64, .f32 => try out.print(alloc, "\tfmul {s}, {s}, {s}\n", .{ dst, lhs, rhs }),
                                        else => try out.print(alloc, "\tmul {s}, {s}, {s}\n", .{ dst, lhs, rhs }),
                                    }
                                },
                                .div => {
                                    switch (binop.dst.type) {
                                        .f64, .f32 => try out.print(alloc, "\tfdiv {s}, {s}, {s}\n", .{ dst, lhs, rhs }),
                                        else => try out.print(alloc, "\tsdiv {s}, {s}, {s}\n", .{ dst, lhs, rhs }),
                                    }
                                },
                                .mod => {
                                    const scratch_reg = try abi.scratchReg(0, .gp);
                                    try out.print(alloc, "\tsdiv {s}, {s}, {s}\n", .{ scratch_reg, lhs, rhs });
                                    try out.print(alloc, "\tmsub {s}, {s}, {s}, {s}\n", .{ dst, scratch_reg, rhs, lhs });
                                },
                                .lshift => {
                                    try out.print(alloc, "\tlsl {s}, {s}, {s}\n", .{ dst, lhs, rhs });
                                },
                                .rshift => {
                                    try out.print(alloc, "\tlsr {s}, {s}, {s}\n", .{ dst, lhs, rhs });
                                },
                                else => |op| {
                                    std.debug.print("op is not supported {s}\n", .{@tagName(op)});
                                    return error.NotSupported;
                                },
                            }
                        },
                        .branch => |b| {
                            const cond = try abi.regFor(b.condition.operand, colors);
                            try out.print(alloc, "\tcmp {s}, #0\n", .{cond});
                            try out.print(alloc, "\tb.ne _{s}_L{d}\n", .{ function.label, b.then_block });
                            try out.print(alloc, "\tb _{s}_L{d}\n", .{ function.label, b.else_block });
                        },
                        .jump => |j| {
                            try out.print(alloc, "\tb _{s}_L{d}\n", .{ function.label, j.target });
                        },
                        .compare => |c| {
                            const dst = try abi.regFor(c.dst.operand, colors);
                            switch (c.lhs.type) {
                                .f64, .f32 => {
                                    const lhs = try abi.regFor(c.lhs.operand, colors);
                                    const rhs = try abi.regFor(c.rhs.operand, colors);
                                    try out.print(alloc, "\tfcmp {s}, {s}\n", .{ lhs, rhs });
                                    try out.print(alloc, "\tcset {s}, {s}\n", .{ dst, c.op.condForCmp() });
                                },
                                else => {
                                    const lhs = try abi.regFor(c.lhs.operand, colors);
                                    const rhs = try abi.regFor(c.rhs.operand, colors);
                                    try out.print(alloc, "\tcmp {s}, {s}\n", .{ lhs, rhs });
                                    try out.print(alloc, "\tcset {s}, {s}\n", .{ dst, c.op.condForCmp() });
                                },
                            }
                        },
                        .select => |s| {
                            const dst = try abi.regFor(s.dst.operand, colors);
                            const scratch_reg = try abi.scratchReg(0, .gp);
                            const if_reg = try valueToReg(s.if_value, out, scratch_reg, colors, abi, alloc);
                            const scratch_reg_2 = try abi.scratchReg(1, .gp);
                            const else_reg = try valueToReg(s.else_value, out, scratch_reg_2, colors, abi, alloc);

                            const condition = try abi.regFor(s.condition.operand, colors);
                            try out.print(alloc, "\tcmp {s}, #0\n", .{condition});
                            try out.print(alloc, "\tcsel {s}, {s}, {s}, ne\n", .{ dst, if_reg, else_reg });
                        },
                        .unaryop => |u| {
                            switch (u.op) {
                                .neg => switch (u.dst.type) {
                                    .f64, .f32 => {
                                        const dst = try abi.regFor(u.dst.operand, colors);
                                        const src = try abi.regFor(u.src.operand, colors);
                                        try out.print(alloc, "\tfneg {s}, {s}\n", .{ dst, src });
                                    },
                                    else => {
                                        const dst = try abi.regFor(u.dst.operand, colors);
                                        const src = try abi.regFor(u.src.operand, colors);
                                        try out.print(alloc, "\tneg {s}, {s}\n", .{ dst, src });
                                    },
                                },
                                .exp2 => return error.NotImpl,
                            }
                        },
                        .cast => |c| {
                            const dst = try abi.regFor(c.dst.operand, colors);
                            const src = try abi.regFor(c.src.operand, colors);
                            // type a -> type b
                            switch (c.src.type) {
                                .i64 => switch (c.dst_target_type) {
                                    .f64 => try out.print(alloc, "\tscvtf {s}, {s}\n", .{ dst, src }),
                                    else => {
                                        std.debug.print("unsupported cast: {s} -> {s}\n", .{
                                            @tagName(c.src.type),
                                            @tagName(c.dst_target_type),
                                        });
                                        return error.UnsupportedCast;
                                    },
                                },
                                .i32 => switch (c.dst_target_type) {
                                    .i64 => try out.print(alloc, "\tsxtw {s}, {s}\n", .{ dst, src }),
                                    else => {
                                        std.debug.print("unsupported cast: {s} -> {s}\n", .{
                                            @tagName(c.src.type),
                                            @tagName(c.dst_target_type),
                                        });
                                        return error.UnsupportedCast;
                                    },
                                },
                                .f64 => switch (c.dst_target_type) {
                                    .i64 => try out.print(alloc, "\tfcvtzs {s}, {s}\n", .{ dst, src }),
                                    else => {
                                        std.debug.print("unsupported cast: {s} -> {s}\n", .{
                                            @tagName(c.src.type),
                                            @tagName(c.dst_target_type),
                                        });
                                        return error.UnsupportedCast;
                                    },
                                },
                                else => {
                                    std.debug.print("unsupported cast: {s} -> {s}\n", .{
                                        @tagName(c.src.type),
                                        @tagName(c.dst_target_type),
                                    });
                                    return error.UnsupportedCast;
                                },
                            }
                        },
                        .stack_alloc => |sa| {
                            const dst = try abi.regFor(sa.dst.operand, colors);
                            next_stack_alloc_byte += sa.bytes;
                            const bytes = local_count * 8 + next_stack_alloc_byte;

                            try out.print(alloc, "\tsub {s}, x29, #{d}\n", .{ dst, bytes });
                        },
                        else => |lir| {
                            std.debug.panic("ir instruction doesnt have a mapping in arm backend: {s}\n", .{@tagName(lir)});
                            return error.NotSupported;
                        },
                    }
                },
                .len => |l| {
                    const dst = try abi.regFor(l.dst.operand, colors);
                    const src = try abi.regFor(l.value.operand, colors);
                    switch (l.value.type) {
                        .list => {
                            try out.print(alloc, "\tldr {s}, [{s}]\n", .{ dst, src });
                        },
                        else => |e| {
                            std.debug.print("len called on {s} unexpectedly\n", .{@tagName(e)});
                            return error.InvalidLenCall;
                        },
                    }
                },
                // abi specific component are handled in pre_color
                .function_call => |fc| {
                    switch (fc.callee) {
                        .direct => |function_name| {
                            try out.print(alloc, "\tbl _{s}\n", .{function_name});
                        },
                        .indirect => |ind| {
                            const addr = try abi.regFor(ind.operand, colors);
                            try out.print(alloc, "\tblr {s}\n", .{addr});
                        },
                    }
                },
                // abi specific component are handled in pre_color
                .function_return => {
                    try out.print(alloc, "\tb _{s}_epilogue\n", .{function.name});
                },
                .function_ref => |fr| {
                    const dst = try abi.regFor(fr.dst.operand, colors);
                    try out.print(alloc, "\tadrp {s}, _{s}@PAGE\n", .{ dst, fr.label });
                    try out.print(alloc, "\tadd {s}, {s}, _{s}@PAGEOFF\n", .{ dst, dst, fr.label });
                },
                else => |ir| {
                    std.debug.panic("ir instruction doesnt have a mapping in arm backend: {s}\n", .{@tagName(ir)});
                    return error.NotSupported;
                },
            }
        }
        if (block.successors.items.len == 0) {
            try out.print(alloc, "\tb _{s}_epilogue\n", .{function.label});
        }
    }
    try createFunctionFooter(out, function.label, frame_stack_size, is_main, abi, alloc);
}

fn createProgramHeader(out: *ArrayList(u8), alloc: std.mem.Allocator) !void {
    try out.appendSlice(alloc, ".section __TEXT,__text\n");
    try out.appendSlice(alloc, ".global _main\n");
}

fn createFunctionHeader(out: *ArrayList(u8), name: []const u8, local_stack_size: usize, abi: Abi, alloc: std.mem.Allocator) !void {
    try out.print(alloc, "_{s}:\n", .{name});
    try out.appendSlice(alloc, "\tstp x29, x30, [sp, #-16]!\n");
    try out.appendSlice(alloc, "\tmov x29, sp\n");
    if (local_stack_size > 0) {
        try out.print(alloc, "\tsub sp, sp, #{d}\n", .{local_stack_size});
    }
    try saveCalleeSaveReg(out, abi, alloc);
}

fn saveCalleeSaveReg(out: *ArrayList(u8), abi: Abi, alloc: std.mem.Allocator) !void {
    // FIXME: talk through abi apis instead
    std.debug.assert(abi.gp_callee_save_regs.len % 2 == 0);
    var i: usize = 0;
    while (i < abi.gp_callee_save_regs.len) : (i += 2) {
        const reg1 = abi.gp_callee_save_regs[i];
        const reg2 = abi.gp_callee_save_regs[i + 1];
        try out.print(alloc, "\tstp {s}, {s}, [sp, #-16]!\n", .{ reg1, reg2 });
    }
}

fn restoreCallleeSafeReg(out: *ArrayList(u8), abi: Abi, alloc: std.mem.Allocator) !void {
    // FIXME: talk through abi apis instead
    std.debug.assert(abi.gp_callee_save_regs.len % 2 == 0);
    var i: usize = abi.gp_callee_save_regs.len;
    while (i > 0) {
        i -= 2;
        const reg1 = abi.gp_callee_save_regs[i];
        const reg2 = abi.gp_callee_save_regs[i + 1];
        try out.print(alloc, "\tldp {s}, {s}, [sp], #16\n", .{ reg1, reg2 });
    }
}

fn emitStackLoad(
    out: *ArrayList(u8),
    dst: []const u8,
    offset: usize,
    scratch: []const u8,
    alloc: std.mem.Allocator,
) !void {
    if (offset <= 256) {
        try out.print(alloc, "\tldr {s}, [x29, #-{d}]\n", .{ dst, offset });
    } else {
        try out.print(alloc, "\tsub {s}, x29, #{d}\n", .{ scratch, offset });
        try out.print(alloc, "\tldr {s}, [{s}]\n", .{ dst, scratch });
    }
}

fn emitStackLoadByte(
    out: *ArrayList(u8),
    dst: []const u8,
    offset: usize,
    scratch: []const u8,
    alloc: std.mem.Allocator,
) !void {
    std.debug.assert(dst[0] == 'x');
    if (offset <= 256) {
        try out.print(alloc, "\tldrb w{s}, [x29, #-{d}]\n", .{ dst[1..], offset });
    } else {
        try out.print(alloc, "\tsub {s}, x29, #{d}\n", .{ scratch, offset });
        try out.print(alloc, "\tldrb w{s}, [{s}]\n", .{ dst[1..], scratch });
    }
}

fn emitStackStore(
    out: *ArrayList(u8),
    src: []const u8,
    offset: usize,
    scratch: []const u8,
    alloc: std.mem.Allocator,
) !void {
    if (offset <= 256) {
        try out.print(alloc, "\tstr {s}, [x29, #-{d}]\n", .{ src, offset });
    } else {
        try out.print(alloc, "\tsub {s}, x29, #{d}\n", .{ scratch, offset });
        try out.print(alloc, "\tstr {s}, [{s}]\n", .{ src, scratch });
    }
}

fn emitStackStoreByte(
    out: *ArrayList(u8),
    src: []const u8,
    offset: usize,
    scratch: []const u8,
    alloc: std.mem.Allocator,
) !void {
    if (offset <= 256) {
        try out.print(alloc, "\tstrb w{s}, [x29, #-{d}]\n", .{ src[1..], offset });
    } else {
        try out.print(alloc, "\tsub {s}, x29, #{d}\n", .{ scratch, offset });
        try out.print(alloc, "\tstrb w{s}, [{s}]\n", .{ src[1..], scratch });
    }
}

fn createFunctionFooter(out: *ArrayList(u8), name: []const u8, local_stack_size: usize, is_main: bool, abi: Abi, alloc: std.mem.Allocator) !void {
    try out.print(alloc, "_{s}_epilogue:\n", .{name});
    if (is_main) {
        // return 0 at end of main
        try out.appendSlice(alloc, "\tmov w0, #0\n");
    }

    try restoreCallleeSafeReg(out, abi, alloc);
    if (local_stack_size > 0) {
        try out.print(alloc, "\tadd sp, sp, #{d}\n", .{local_stack_size});
    }

    // restore frame pointer and return address
    try out.appendSlice(alloc, "\tldp x29, x30, [sp], #16\n");
    try out.appendSlice(alloc, "\tret\n");
}

fn createFooter(out: *ArrayList(u8), alloc: std.mem.Allocator) !void {
    try out.appendSlice(alloc, "\n.section __TEXT,__cstring\n");
    try out.appendSlice(alloc, "fmt:\n");
    try out.appendSlice(alloc, "\t.asciz \"%ld\\n\"\n");
}

fn countLocals(blocks: *const ArrayList(Block)) usize {
    var max_local: ?common.ir.LocalId = null;
    for (blocks.items) |block| {
        for (block.instructions.items) |instruction| {
            switch (instruction) {
                .lir => |l| {
                    switch (l) {
                        .store_local => |sl| {
                            max_local = if (max_local) |m| @max(m, sl.local.id) else sl.local.id;
                        },
                        .load_local => |ll| {
                            max_local = if (max_local) |m| @max(m, ll.local.id) else ll.local.id;
                        },
                        else => {},
                    }
                },
                else => {},
            }
        }
    }
    return if (max_local) |m| @as(usize, m) + 1 else 0;
}

fn countStackAllocBytes(blocks: *const ArrayList(Block)) usize {
    var slots: usize = 0;
    for (blocks.items) |block| {
        for (block.instructions.items) |instruction| {
            switch (instruction) {
                .lir => |lir| switch (lir) {
                    .stack_alloc => |sa| {
                        slots += sa.bytes;
                    },
                    else => {},
                },
                else => {},
            }
        }
    }
    return slots;
}

fn localOffset(local: common.ir.LocalId) usize {
    return (@as(usize, local) + 1) * 8;
}

fn arrayOffset(local_count: usize, array_slot_index: usize) usize {
    return (local_count + array_slot_index + 1) * 8;
}

fn spillOffset(local_stack_size: usize, slot: usize) usize {
    return local_stack_size + (slot + 1) * 8;
}

fn emitConstantToReg(
    out: *ArrayList(u8),
    dst: []const u8,
    value: ConstValue,
    alloc: std.mem.Allocator,
) !void {
    switch (value) {
        .i64, .i32 => |i| try emitMov(out, dst, i, alloc),
        .char => |c| try out.print(alloc, "\tmov {s}, #{d}\n", .{ dst, c }),
        .bool => |b| try out.print(alloc, "\tmov {s}, #{d}\n", .{ dst, @intFromBool(b) }),
        .float => return error.NotImpl,
    }
}

fn emitMov(out: *ArrayList(u8), dst: []const u8, value: i64, alloc: std.mem.Allocator) !void {
    if (value < 0) {
        const positive: u64 = @intCast(-value);
        try emitMovUnsigned(out, dst, positive, alloc);
        try out.print(alloc, "\tneg {s}, {s}\n", .{ dst, dst });
        return;
    }
    try emitMovUnsigned(out, dst, @intCast(value), alloc);
}

fn emitMovUnsigned(out: *ArrayList(u8), dst: []const u8, value: u64, alloc: std.mem.Allocator) !void {
    // [0..] [0..] [0..] [<lower>]
    const lower: u16 = @truncate(value);
    // also zeros out other portions
    try out.print(alloc, "\tmovz {s}, #{d}\n", .{ dst, lower });
    // [<64>] [<32>] [<16>] [<done>]
    inline for (.{ 16, 32, 48 }) |shift| {
        const shifted_value: u16 = @truncate(value >> shift);
        if (shifted_value != 0) {
            try out.print(alloc, "\tmovk {s}, {d}, lsl #{d}\n", .{ dst, shifted_value, shift });
        }
    }
}

// TODO: emitMov is more generic than this
fn valueToReg(
    value: ValueRef,
    out: *std.ArrayList(u8),
    cur_scratch_reg: []const u8,
    colors: *const ColoredGraph,
    abi: Abi,
    alloc: std.mem.Allocator,
) ![]const u8 {
    switch (value) {
        .top => |top| return abi.regFor(top.operand, colors),
        .constant => |c| {
            switch (c) {
                .f64, .f32 => |f| {
                    try out.print(alloc, "fmov {s}, #{}\n", .{ cur_scratch_reg, f });
                    return cur_scratch_reg;
                },
                .i32, .i64 => |i| {
                    try out.print(alloc, "mov {s}, #{d}\n", .{ cur_scratch_reg, i });
                    return cur_scratch_reg;
                },
                else => |e| {
                    std.debug.print("cant handle {s}\n", .{@tagName(e)});
                    return error.NotImpl;
                },
            }
        },
    }
}

fn getRegPrefix(size: usize) ![]const u8 {
    return switch (size) {
        1, 4 => "w",
        8 => "x",
        else => error.NotImpl,
    };
}
