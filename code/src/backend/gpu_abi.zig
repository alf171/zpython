const std = @import("std");
const Operand = @import("common").alloc.Operand;
const ValueRef = @import("common").ir.ValueRef;
const ColoredGraph = @import("middle").color.ColoredGraph;
const TypeInfo = @import("common").types.TypeInfo;
const RegisterFile = @import("common").register.RegisterFile;
const RegisterType = @import("common").register.RegisterType;

pub const RegisterUsage = struct {
    vgpr_next: u16,
    sgpr_next: u16,
};

pub const GpuReg = struct {
    reg_type: RegisterType,
    base: u16,
    count: u8,

    pub fn toString(self: @This(), alloc: std.mem.Allocator) ![]const u8 {
        const reg_type = switch (self.reg_type) {
            .vgpr => "v",
            .sgpr => "s",
            else => unreachable,
        };
        return try std.fmt.allocPrint(alloc, "{s}{d}", .{ reg_type, self.base });
    }

    pub fn toStringAt(self: @This(), offset: u8, alloc: std.mem.Allocator) ![]const u8 {
        const reg_type = switch (self.reg_type) {
            .vgpr => "v",
            .sgpr => "s",
            else => unreachable,
        };
        return try std.fmt.allocPrint(alloc, "{s}{d}", .{ reg_type, self.base + offset });
    }
};

pub const GpuAbi = struct {
    sgpr_allocatable_regs: []const u16,
    vgpr_allocatable_regs: []const u16,
    sgpr_scratch_regs: []const u16,
    vgpr_scratch_regs: []const u16,

    pub fn init(
        sgpr_allocatable_regs: []const u16,
        vgpr_allocatable_regs: []const u16,
        sgpr_scratch_regs: []const u16,
        vgpr_scratch_regs: []const u16,
    ) @This() {
        return .{
            .sgpr_allocatable_regs = sgpr_allocatable_regs,
            .vgpr_allocatable_regs = vgpr_allocatable_regs,
            .sgpr_scratch_regs = sgpr_scratch_regs,
            .vgpr_scratch_regs = vgpr_scratch_regs,
        };
    }

    fn regForFromIndex(self: @This(), index: usize, reg_type: RegisterType) !u16 {
        const allocatable_regs = switch (reg_type) {
            .vgpr => self.vgpr_allocatable_regs,
            .sgpr => self.sgpr_allocatable_regs,
            else => unreachable,
        };
        if (index >= allocatable_regs.len) return error.TooManyArgs;
        return allocatable_regs[index];
    }

    pub fn regFor(self: @This(), op: Operand, colors: *const ColoredGraph) !GpuReg {
        switch (op) {
            .temp => {
                const node = colors.nodes.get(op) orelse {
                    std.debug.print("Missing color for operand: ", .{});
                    op.print();
                    std.debug.print("\n", .{});
                    return error.MissingColor;
                };
                const reg_id = node.register orelse return error.MissingColor;
                const idx = try regForFromIndex(self, reg_id, node.reg_class.type);
                return .{
                    .reg_type = node.reg_class.type,
                    .base = idx,
                    .count = node.reg_class.count,
                };
            },
            .reg => |reg| {
                return .{
                    .reg_type = reg.type,
                    .base = reg.id,
                    .count = reg.count(),
                };
            },
            else => return error.UnsupportedOperand,
        }
    }

    pub fn scratchReg(self: @This(), index: usize, count: u8, reg_type: RegisterType) !GpuReg {
        const regs = switch (reg_type) {
            .vgpr => self.vgpr_scratch_regs,
            .sgpr => self.sgpr_scratch_regs,
            else => unreachable,
        };

        if (count == 0 or index + count > regs.len)
            return error.InvalidScratchReg;

        const base = regs[index];
        return .{
            .base = base,
            .count = count,
            .reg_type = reg_type,
        };
    }

    pub fn registerFiles(self: @This()) [2]RegisterFile {
        return .{
            .{
                .count = @intCast(self.vgpr_allocatable_regs.len),
                .type = .vgpr,
                .forbidden_mask = 0,
            },
            .{
                .count = @intCast(self.sgpr_allocatable_regs.len),
                .type = .sgpr,
                .forbidden_mask = 0,
            },
        };
    }

    pub fn registerUsage(self: @This(), colors: *const ColoredGraph) !RegisterUsage {
        var usage: RegisterUsage = .{
            // v0 contains work items (x=bits[0-9],y=bits[10,19],z=bits[20-20])
            .vgpr_next = 1,
            //s[0:1] = kernarg pointer, s[2] work group
            .sgpr_next = 4,
        };

        var it = colors.nodes.iterator();
        while (it.next()) |entry| {
            const node = entry.value_ptr.*;
            const color = node.register orelse continue;

            const base = try self.regForFromIndex(color, node.reg_class.type);
            const next = base + node.reg_class.count;

            switch (node.reg_class.type) {
                .vgpr => usage.vgpr_next = @max(usage.vgpr_next, next),
                .sgpr => usage.sgpr_next = @max(usage.sgpr_next, next),
                else => unreachable,
            }
        }

        for (self.vgpr_scratch_regs) |reg| {
            usage.vgpr_next = @max(usage.vgpr_next, reg + 1);
        }

        for (self.sgpr_scratch_regs) |reg| {
            usage.sgpr_next = @max(usage.sgpr_next, reg + 1);
        }
        return usage;
    }
};
